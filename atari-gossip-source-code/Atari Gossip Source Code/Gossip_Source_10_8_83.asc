ARROW	DB	$08,$1C,$3E,$6B,$08,$08,$08,$08,$08,$08

PMHCTR	DB	44,84,124,164,36,76,116,156

NXRGHT	INY
	CPY	PLYERS
	BMI	CTRGHT
	LDY	#0
	JMP	NXRGHT

CTRGHT	LDA	(ATIMES),Y
	BNE	NXRGHT
	STY	TOPIC
	RTS

NXLEFT	DEY
	BNE	CTLEFT
	LDY	PLYERS
	JMP	NXLEFT

CTLEFT	LDA	(ATIMES),Y
	BNE	NXLEFT
	STY	TOPIC
	RTS

;************************************
;       ABOUT WHOM TO TALK?
;************************************

CHSTPC	JSR	BLNKYS
	LDA	#HIGH ATIMAD
	STA	ATIMES+1
	LDA	#LOW ATIMAD+1
	STA	ATIMES
	LDA	#1
	STA	SIZEP2
	LDA	#15
	STA	PCOLR2

	LDA	#255
	STA	CH
	LDA	#2
	JSR	DELAY2
	LDA	CH
	CMP	#12
	BEQ	CHSTPC

	LDA	TRIG0
	BEQ	CHSTPC
	LDY	#3
	JSR	DMESS
	LDY	CHCALL
	LDA	#1
	STA	(ATIMES),Y

	LDY	#0
	JSR	NXRGHT
	JSR	PUTARW

STKTST	JSR	SELTST
	BNE	STKTCT
	LDX	#0
	JSR	RSFONE
	LDX	CHCALL
	JSR	RSFONE
	JMP	CHSTPC

STKTCT	LDA	CH
	CMP	#7
	BEQ	RIGHT1

	LDA	STICK0
	CMP	#7
	BNE	LEFTST

RIGHT1	LDY	TOPIC
	JSR	NXRGHT
	JMP	CHOUT

LEFTST	LDA	CH
	CMP	#6
	BEQ	LEFT1

	LDA	STICK0
	CMP	#11
	BNE	NOJOYS

LEFT1	LDY	TOPIC
	JSR	NXLEFT

CHOUT	LDA	#255
	STA	CH

	JSR	PUTARW
	JSR	BEEP
	LDY	#40
	JSR	YLOOP

NOJOYS	LDA	00020
	STA	PCOLR2

	LDA	CH
	CMP	#12
	BEQ	RET2

	LDA	TRIG0
	BNE	STKTST

RET2	JSR	BEEP
TDBNCE	LDA	00020
	STA	PCOLR2
	LDA	TRIG0
	BEQ	TDBNCE

KBNCE2	LDA	#255
	STA	CH
	LDA	#2
	JSR	DELAY2

	LDA	CH
	CMP	#12
	BEQ	KBNCE2
	LDA	#0
	STA	ATTRACT
	STA	HPOSP2
	LDA	#LOW ATIMAD
	STA	ATIMES
	JMP	STPCLK

;***********************************
;       CHOSE WHOM TO CALL
;***********************************

CHSWHM	JSR	BLNKYS
	LDX	#0
	JSR	RSFONE
	LDA	TRIG0
	BEQ	CHSWHM

	LDA	#255
	STA	CH
	LDA	#2
	JSR	DELAY2
	LDA	CH
	CMP	#12
	BEQ	CHSWHM

	LDX	#1
	STX	CHCALL
	LDY	#2
	JSR	DMESS
	JSR	PUTBOX


STKTS2	JSR	SELTST
	BEQ	CHSWHM

	LDA	CH
	CMP	#7
	BEQ	RIGHT2

	LDA	STICK0
	CMP	#7
	BNE	LFTEST

RIGHT2	INC	CHCALL
	JMP	CHOUT3

LFTEST	LDA	CH
	CMP	#6
	BEQ	LEFT2

	LDA	STICK0
	CMP	#11
	BNE	NOJOY3

LEFT2	DEC	CHCALL
CHOUT3	LDA	#255
	STA	CH

	LDA	CHCALL
	BNE	CH3
	LDA	PLYERS
	SEC
	SBC	#1
	JMP	STCALL

CH3	CMP	PLYERS
	BNE	STCALL
	LDA	#1

STCALL	STA	CHCALL
	JSR	PUTBOX
	JSR	BEEP
	LDY	#40
	JSR	YLOOP

NOJOY3	LDA	CH
	CMP	#12
	BEQ	RET3

	LDA	TRIG0
	BNE	STKTS2

RET3	LDA	#0
	STA	ATTRACT
	STA	HPOSP2
	LDX	CHCALL
	JSR	RNGFON
	JSR	RSFONE
	STX	TALKER
	STX	LSTCLL
	JSR	HELLO
	LDY	#100
	JSR	YLOOP
	LDX	#0
	JSR	DTALK
	JMP	STPCLK

CLRMIS	LDA	#128
	STA	ARG5
	LDY	#127
	LDA	#PMBASE/256+1
	STA	ARG5HI
	LDA	#0
	JMP	CLROOP

CLRPM2	LDA	#0
	JMP	CLRP23

CLRPM3	LDA	#128

CLRP23	STA	ARG5
	LDY	#127
	LDA	#PMBASE/256+3
	STA	ARG5HI
	LDA	#0

CLROOP	STA	(ARG5),Y
	DEY
	BNE	CLROOP
	RTS


PUTARW	JSR	CLRPM2
	LDA	#PMBASE/256+3
	STA	ARG1HI
	LDA	TOPIC
	CMP	#4
	BPL	LOWER
	LDA	#20
	JMP	SETLOW

LOWER	LDA	#65
SETLOW	CLC
	ADC	#10

	STA	ARG1
	LDY	#9

ALOOP	LDA	ARROW,Y
	STA	(ARG1),Y
	DEY
	BPL	ALOOP
	LDX	TOPIC
	LDA	PMHCTR,X
	CLC
	ADC	#19
	STA	HPOSP2
	RTS


BOX	 DB $FF,$81,$81,$81,$81,$81,$81,$81,$81,$81,$81,$FF

PUTBOX	JSR	CLRPM2
	LDA	#1
	STA	SIZEP2
	LDA	#PMBASE/256+3
	STA	ARG1HI
	LDA	CHCALL
	CMP	#4
	BPL	LOWER3
	LDA	#20+20
	JMP	STLOW3

LOWER3	LDA	#64+20
STLOW3	CLC
	ADC	#10

	STA	ARG1
	LDY	#11

BOXLOP	LDA	BOX,Y
	STA	(ARG1),Y
	DEY
	BPL	BOXLOP
	LDX	CHCALL
	LDA	PMHCTR,X
	CLC
	ADC	#29
	STA	HPOSP2
	LDA	#15
	STA	PCOLR2
	RTS

QUOTE  DB $FF,$FF,$FF,$C3,$C3,$C3

PUTQUT	JSR	CLRMIS
	LDA	#PMBASE/256+1
	STA	ARG1HI
	LDA	QUOTEE
	CMP	#4
	BPL	LOWERM
	LDA	#20
	JMP	STLOWM

LOWERM	LDA	#64
STLOWM	CLC
	ADC	#125

	STA	ARG1
	LDY	#3

QUOLOP	LDA	QUOTE,Y
	STA	(ARG1),Y
	DEY
	BPL	QUOLOP
	LDX	QUOTEE
	LDA	PMHCTR,X
	CLC
	ADC	#12
	STA	HPOSM0
	ADC	#2
	STA	HPOSM1
	ADC	#26
	STA	HPOSM2
	ADC	#2
	STA	HPOSM3
	RTS

BEEP	LDA	#0
	STA	ATTRACT
	LDA	#170
	STA	AUDC1
	LDA	#100
	STA	AUDF1

	LDA	#3
	JSR	DELAY2

	LDA	#0
	STA	AUDC1
	RTS

SLGNDR	LDY	#7
	JSR	DMESS
	JSR	DEBNCE
SLOOP	LDA	CONSOL
	AND	#2
	BNE	LTEST
	INC	GENDER
	JSR	BEEP
	LDA	GENDER
	AND	#1
	STA	GENDER
	JSR	DGNDER
	JSR	DEBNCE
LTEST 	LDA	CONSOL
	AND	#1
	BNE	SLOOP
	STA	ATTRACT
	RTS

DGNDER	JSR	CLRPM3
	LDA	GENDER
	BNE	DYURHR
	STA	HPOSP3
	RTS

DYURHR	LDY	#15
HLOOP	LDA	HAIR2,Y
	STA	PMBASE+907,Y
	DEY
	BPL	HLOOP
	LDA	#3
	STA	SIZEP3
	LDA	#58
	STA	PCOLR3
	LDA	#56
	STA	HPOSP3
	RTS


NOKILI	LDA	#0
	STA	DLVCTR
STNOKL	LDA	#1
	TAY
	LDX	#0
	JSR	SETVBV
	LDA	#192
	STA	NMIEN
	RTS


;*************************************
;       SELECT # OF PLYERS
;       AND PLYERS GENDER
;*************************************
STPLRS	LDA	#NOKILI&255
	STA	CDTMA1
	LDA	#NOKILI/256
	STA	CDTMA1+1
	JSR	STNOKL

	LDA	#HIGH ATIMAD
	STA	ATIMES+1
	LDA	#LOW ATIMAD
	STA	ATIMES

	LDA	#1
	STA	TURN      ;FIRST TURN
	STA	SOURCE
	LDA	#5
	STA	PLYERS
	LDA	#0
	STA	GENDER

OPNING	JSR	MAIN
	JSR	BLNKYS

	LDA	SOURCE
	BEQ	OPCONT
	JSR	CREDTS
	LDA	#0
	STA	SOURCE

OPCONT	JSR	SLGNDR
	JSR	DEBNCE
	LDA	GENDER
	BEQ	INTMED
	LDA	#$88
	STA	HAIRP1
	LDA	#$FF
	STA	HAIRP2
	LDA	#$58
	STA	HAIRP3
	LDA	#$A8
	STA	HAIRP4
INTMED	JSR	MAIN
	LDY	#1
	JSR	DMESS
	JSR	DEBNCE

OPLOOP	LDA	CONSOL
	AND	#2
	BNE	STRTST
	INC	PLYERS
	JSR	BEEP
	LDA	PLYERS
	CMP	#9
	BEQ	BAKTO4

	LDX	PLYERS
	DEX
	LDA	#0
	STA	CURFAC
	JSR	PUTALL
	JSR	PHAIR

	JSR	DEBNCE
	JMP	OPLOOP

BAKTO4	LDA	#5
	STA	PLYERS
	JMP	INTMED
STRTST	LDA	CONSOL
	AND	#1
	BNE	OPLOOP
	LDA	#0
	STA	ATTRACT
	JSR	STPCLK


	LDY	PLYERS
	LDA	ESPTAB,Y
	STA	NUMESP
	RTS

ESPTAB	DB	0,0,0,0,2,4,9,16,25
;***********************************
;       CHANGE FACES WHILE TALKING
;***********************************
CHFACE	TXA
	PHA
	CMP	#0
	BNE	CHCONT
	LDA	GENDER

CHCONT	EOR	#1
	AND	#1
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	STA	FSHIFT

	LDA	#1
	STA	MODE
	LDA	#0
	STA	DUR
	LDA	#100
	STA	JIFFYL
	LDA	#0
	STA	JIFFYH
	JSR	STRTIN
	PLA
	TAX

CHLOOP	LDA	RANDOM
	AND	#3
	STA	CURFAC
	JSR	DFACE
	JSR	DELAY
	LDA	STATUS
	BEQ	CHLOOP
	LDA	#0
	STA	CURFAC
	JMP	DFACE

;***********************************
;       SAY GOOD BYE
;***********************************
BYEBYE	LDY	#100
	JSR	YLOOP
	LDA	#0
	STA	FIRST
	LDA	#40
	STA	LAST
	JMP	BABBLE

;***********************************
;       SAY HELLO
;***********************************
HELLO	LDA	#60
	STA	FIRST
	LDA	#85
	STA	LAST

BABBLE	LDA	TALKER	;DEEPEN MALE VCE
	BNE	BACONT
	LDA	GENDER

BACONT	EOR	#1
	AND	#1
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	STA	FSHIFT

	LDA	#0
	STA	MODE
	STA	DUR
	JSR	STRTIN

HELOOP	LDX	TALKER
	LDA	RANDOM
	AND	#3
	STA	CURFAC
	JSR	DFACE
	JSR	DELAY
	LDA	STATUS
	BEQ	HELOOP
	LDA	#0
	STA	CURFAC
	JMP	DFACE

;************************************
;         YAK3
;************************************

STRTIN	LDA #0
	STA STATUS	;SET HV BEGUN FLG
	STA AUDCTL
	LDA #3
	STA SKCTL
	LDA #$EF
	STA AUDC1
	STA AUDC2
	STA AUDC3
	STA AUDC4
	LDA MODE
	BNE INIRAN
	LDA FIRST	;0 MODE-NOT RNDOM
	STA POINTR
	LDA #$FF
	STA JIFFYL
	STA JIFFYH
	JMP FKSTVB
INIRAN	LDA #0	;1 MODE-RANDOM
	STA FIRST
	STA LAST
	STA POINTR
FKSTVB	LDX #HIGH RANDY
	LDY #LOW RANDY
	LDA #7
	JMP SETVBV

RANDY	PHA
	TXA
	PHA
	TYA
	PHA
	LDA	POINTR
	CMP	LAST
	BNE	PLAYY
	LDA	MODE
	BNE	SKIP
	JMP	OFFY

SKIP	LDA	RANDOM
	CLC
	ADC	#96
	BCC	RST1
	ADC	#95
RST1	STA	FIRST
	LDA	RANDOM
	CLC
	ADC	#96
	BCC	RST2
	ADC	#95
RST2	STA	LAST

	BCS	RST3	;IF SO, BRANCH.
	PHA		;IF NOT, SWAP
	LDA	FIRST	;FIRST AND LAST
	STA	LAST
	PLA
	STA	FIRST
RST3	LDA	LAST
	CMP	FIRST	;IS FIRST = LAST?
	BNE	RST4	;IF NOT,NO WORRY
	INC	LAST	;ELSE RAISE LAST.
RST4	LDA	FIRST
	STA	POINTR
PLAYY	LDY	POINTR
	LDA	RANDOM
	CMP	DUR
	BCC	BEXIT	;BRNCH IF ACC<DUR
	LDA	FTAB1,Y	;ELSE NEW NOTES
	CLC
	ADC	FSHIFT	;SHIFT FREQUENCY
	STA	TEMP
	STA	AUDF1
	LDA	VTAB1,Y
	STA	AUDC1
;V2
	LDA	TEMP
	CLC
	ADC	#$FF
	LSR	A
	STA	TEMP
	STA	AUDF2
	LDA	VTAB2,Y
	STA	AUDC2
;V3
	LDA	TEMP
	CLC
	ADC	#$FF
	LSR	A
	STA	TEMP
	STA	AUDF3
	LDA	VTAB3,Y
	STA	AUDC3
;V4
	LDA	TEMP
	CLC
	ADC	#$FF
	LSR	A
	STA	AUDF4
	LDA	VTAB4,Y
	STA	AUDC4
	INC	POINTR; SET FOR NEXT TIME

BEXIT	DEC	JIFFYL
	BNE	EX2
	DEC	JIFFYH
	BMI	OFFY

EX2	PLA
	TAY
	PLA
	TAX
	PLA
	JMP	XITVBL ;RETURN FROM INT'PT

OFFY	LDA	#7
	LDX	#$E4
	LDY	#$62
	JSR	SETVBV	;RESET VB VECTOR
			;TO NORMAL
	LDA	#$FF
	STA	STATUS	;"DONE" FLAG SET
	LDA	#$E0
	STA	AUDC1	;TURN OFF 
			;OSCILLATORS
	STA	AUDC2
	STA	AUDC3
	STA	AUDC4
	PLA	
	TAY
	PLA
	TAX
	PLA
	JMP	XITVBL ;RETURN FROM INT'PT


;************************************
;     OPENING CREDITS FOR GOSSIP
;************************************

OPNTXT	DB	'                    GOSSIP      BY CHRIS CRAWFORD, KIM WHITMORE AND ARIC WILMUNDER          '
	DB	'COPYRIGHT(C) 1983 ATARI INC.     PRESS START TO PLAY                     '

CREDTS	LDA	#OPNTXT/256
	STA	ARG6HI
	LDA	#OPNTXT&255
	STA	ARG6
	LDA	#1
	STA	SELMSK

	LDA	#100
	JSR	DELAY2

CR9LUP	LDA	#$FF
	STA	STATUS

	LDA	ARG6HI
	STA	ARG2HI
	LDA	ARG6
	STA	ARG2
	JSR	PLINE
	LDA	#143
	STA	CNTR

CROTLP	LDA	#7
	STA	YPOS

CRCLUP	LDA	YPOS
	STA	HSCROL
	LDA	#1
	JSR	DELAY2

	LDA	STATUS
	BNE	FLAT
	LDA	RANDOM
	AND	#3
	STA	CURFAC
	LDX	#0
	JSR	DFACE
	JMP	STCHEK

FLAT	LDA	#0
	STA	CURFAC
	TAX
	JSR	DFACE

STCHEK	LDA	CONSOL
	AND	SELMSK
	BNE	JNKLBL
	STA	ATTRACT
	LDA	#15
	STA	HSCROL
	LDA	#1
	STA	JIFFYL
	LDA	#0
	STA	JIFFYH
	STA	CURFAC
	LDX	#0
	JMP	DFACE

JNKLBL	DEC	YPOS
	BPL	CRCLUP
	INC	ARG2
	BNE	NOUPPR
	INC	ARG2HI

NOUPPR	JSR	PLINE
	LDA	#7
	STA	HSCROL
	DEC	CNTR
	BNE	CROTLP
	JMP	CR9LUP

;************************************
;         PUT LINE
;************************************
PLINE	LDY	#21
CRLOOP	LDA	(ARG2),Y
	CLC
	ADC	#32
	STA	MAINSR+320,Y
	DEY
	BPL	CRLOOP

;      MAKE TALKING DURING OPENING

	LDY	#21
	LDA	(ARG2),Y	  ;CHAR? Y,SOUND
	CMP	#32
	BNE	STSOND

;      STOP SOUND

	LDA	#1
	STA	JIFFYL
	LDA	#0
	STA	JIFFYH
	RTS

STSOND	LDA	STATUS
	BNE	CNTSND	;SOUND ON? Y,RTS
	RTS

CNTSND	LDA	GENDER
	EOR	#1
	AND	#1
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	STA	FSHIFT

	LDA	#1
	STA	MODE
	LDA	#64
	STA	DUR
	LDA	#100
	STA	JIFFYL
	STA	JIFFYH
	JMP	STRTIN
;***********************************
;	  CREATE MAIN DISPLAY
;***********************************
MAINSR	=	SCREEN+2
MATSCR	=	MAINSR

PLYERS	=	N	;NUM OF PLAYERS
CHCALL	=	J	;CALLEE

HEDLOC	DB	2,7,12,17,141,146,151,156
FACETP	DB	83,85,87,89,91
FACEBT	DB	115,117,119,121,123
NAMLOC	DB	83,93,103,113,221,231,241,251
NAMESG	DB	' YOU',' DAN',' VAL',' JIM'
	DB	' JOE',' AMY',' TOM',' SUE'
NAMESB	DB	' YOU',' VAL',' JIM',' LIZ'
	DB	' AMY',' DAN',' SUE',' TOM'
BODCHR	DB	168,172,173,169,106,174,175,107
	DB	40,32,33,41,106,34,35,107
	DB	232,228,229,233,234,230,231,235
	DB	168,160,161,169,170,162,163,171
	DB	232,224,225,233,106,226,227,107
	DB	168,164,165,169,106,166,167,107
	DB	40,32,33,41,106,34,35,107
	DB	232,228,229,233,106,230,231,107

BODCH1	DB	168,172,173,169,106,174,175,107
	DB	232,228,229,233,234,230,231,235
	DB	40,32,33,41,106,34,35,107
	DB	168,164,165,169,170,166,167,171
	DB	232,228,229,233,106,230,231,107
	DB	40,32,33,41,106,34,35,107
	DB	168,164,165,169,106,166,167,107
	DB	232,224,225,233,106,226,227,107

DEBNCE	LDA	CONSOL
	CMP	#7
	BNE	DEBNCE

	LDA	#7
	JSR	DELAY2
DBOU2	LDA	CONSOL
	CMP	#7
	BNE	DBOU2
	JMP	DELAY

; MESSAGE DISPLAY ROUTINE

MESSGE	DB	'12345678901234567890'
	DB	'   ',147,133,140,133,131,148,' PLAYERS   '	;1
	DB	'    WHOM TO CALL?   '	;2
	DB	' ABOUT WHOM TO TALK?'	;3
	DB	'JAN SAYS - ABOUT AMY'	;4
	DB	'                    '	;5
	DB	'  HOW DO YOU FEEL?  '	;6
	DB '   ',147,133,140,133,131,148,' GENDER    '	;7
	DB	'  NNN WILL NOT TALK '	;8
	DB	'   NNN IS CALLING   '	;9
	DB	'  SORRY, GOTTA GO   '	;10
	DB	' YOU FEEL ABOUT NNN?'	;11
	DB	' NNN FEELS ABOUT NNN'	;12
	DB	' NNN SAYS NNN FEELS '	;13
	DB	' HOW DO YOU RESPOND?'	;14
	DB	'     PLEASE WAIT    '	;15
;PASS Y=MESSAGE #
;FIRST BYTE YPOS=63

DMESS	LDA	#MESSGE/256
	STA	ARG2HI
	LDA	#MESSGE&255
INCLOP	CLC
	ADC	#20
	STA	ARG2
	BCC	DCRY
	INC	ARG2HI
DCRY	DEY
	BNE	INCLOP

	LDY	#19
MESLOP	LDA	(ARG2),Y
	CLC
	ADC	#32
	STA	MAINSR+320,Y
	DEY
	BPL	MESLOP
	RTS

;************************************
;   PUT NAME X IN WINDOW AT YPOS
;************************************

WNDNAM	LDA	#MAINSR/256+1
	STA	ARG1HI
	LDA	#MAINSR&255
	CLC
	ADC	YPOS
	STA	ARG1

	LDA	GENDER
	BEQ	MALNAG
	LDA	#NAMESG/256
	STA	ARG2HI
	LDA	#NAMESG&255
	STA	ARG2
	JMP	NORMGO
MALNAG	LDA	#NAMESB/256
	STA	ARG2HI
	LDA	#NAMESB&255
	STA	ARG2
NORMGO	LDA	NAME
	SEC
	SBC	#1
	ASL	A
	ASL	A
	ADC	ARG2
	STA	ARG2
	BCC	INOCAR
	INC	ARG2HI
INOCAR	LDY	#3
DILOOP	LDA	(ARG2),Y
	CLC
	ADC	#32
	STA	(ARG1),Y
	DEY
	BPL	DILOOP
	RTS


;************************************
;   WHAT DO YOU SAY TO J ABOUT 
;   TOPIC?:INPUT TALK
;************************************

SHWFEL	LDA	#0
	STA	SOURCE
	LDY	#11
	JSR	DMESS
	LDA	#79
	STA	YPOS
	LDA	TOPIC
	STA	NAME
	JSR	WNDNAM
	JMP	TALJMP

;************************************
;  HOW DO YOU RESPOND? INPUT TALK
;************************************

RSPOND	LDA	#1
	STA	SOURCE
	LDY	#14
	JSR	DMESS
TALJMP	DEC	TOPIC
	JSR	PUTARW
	INC	TOPIC
	JSR	DFEEL
	LDX	#0
	STX	CURFAC
	JSR	DFACE
	JMP	CLRPM2

;************************************
;         DISPLAY YOUR FEELINGS
;************************************
DFEEL	LDA	#4
	STA	CURFAC

	LDX	#5
	LDA	#1
	STA	FRMTMS,X

	LDX	#0
	JSR	DFACE
	LDX	#5


FELOOP	LDA	CH
	CMP	#14
	BEQ	UP1
	LDA	STICK0
	CMP	#14
	BNE	STKLFT
UP1	INX
	CPX	#9
	BNE	SHEXIT
	LDX	#8
	JMP	NOBEEP

STKLFT	LDA	CH
	CMP	#15
	BEQ	DOWN1
	LDA	STICK0
	CMP	#13
	BNE	NOJOY
DOWN1	DEX
	CPX	#255
	BNE	SHEXIT
	LDX	#0
	JMP	NOBEEP

SHEXIT	JSR	BEEP

NOBEEP	LDA	#255
	STA	CH
	LDA	#1
	STA	FRMTMS,X
	JSR	SWLOOP
	LDY	#80
	JSR	YLOOP

NOJOY	LDA	00020
	STA	PCOLR2
	JSR	SWLOOP
	LDY	#4
	JSR	YLOOP

	JSR	SELTST
	BNE	TSTTRG

	LDX	#0
	JSR	RSFONE
	LDA	SOURCE
	BEQ	GSHWFL

	LDX	I
	DEX
	JSR	RSFONE
	JMP	RSPOND
GSHWFL	LDX	CHCALL
	DEX
	JSR	RSFONE
	JMP	SHWFEL

TSTTRG	LDA	CH
	CMP	#12
	BEQ	RET1

	LDA	TRIG0
	BEQ	RET1
	JMP	FELOOP
RET1	STX	TALK
	JSR	BEEP

TRGBNC	LDA	00020
	STA	PCOLR2
	LDA	TRIG0
	BEQ	TRGBNC
	JSR	CLRMIS
KYBNCE	LDA	#255
	STA	CH
	LDA	#2
	JSR	DELAY2
	LDA	CH
	CMP	#255
	BNE	KYBNCE
	RTS

;*************************************
;        TEST FOR SELECT PRESSED
;        0 RETURNED IF MATRIX CALLED
;*************************************
SELTST	LDA	CONSOL
	AND	#2
	BEQ	SELCNT
	RTS
SELCNT	JSR	BEEP
	JSR	PSCRN
	JSR	DEBNCE
	JSR	STEST2
	JSR	MAIN
	JMP	DEBNCE

;************************************
;	   B132 I;" IS CALLING" Y=9
;	   PLAYER I RAISES PHONE,
;	   CALLS YOU, YOU RAISE PHONE
;	   AND EXCHANGE GREETINGS?
;************************************
ICLING	LDY	#9
	JSR	DMESS
	LDA	#66
	STA	YPOS
	LDA	I
	STA	NAME
	JSR	WNDNAM
	LDX	I
	DEX
	JSR	RSFONE
	LDX	#0
	JSR	RNGFON
	JSR	RSFONE
	LDA	#0
	STA	TALKER
	JSR	HELLO
	LDY	#100
	JSR	YLOOP
	LDX	I
	DEX

	STX	LSTCLL
	JSR	DTALK
	LDY	#100
	JSR	YLOOP
	LDX	#0
	JMP	DTALK

;************************************
;	   SORRY I MUST GO NOW
;	   LOWER PHONES
;************************************
MUSTGO	JSR	CLRPM2
	LDA	LSTCLL
	CMP	#$FF
	BEQ	MSTXIT

	LDY	#10
	JSR	DMESS
	LDY	#255
	JSR	YLOOP
	LDX	LSTCLL
	STX	TALKER
	JSR	BYEBYE

MSTXIT	LDX	#0
MGLOOP	JSR	LWRFON
	INX
	CPX	PLYERS
	BNE	MGLOOP
	LDA	#$FF
	STA	LSTCLL
	LDY	#255
	JMP	YLOOP

;************************************
;	   I SAYS TALK ABOUT TOPIC
;************************************

ISAYSA	LDX	I
	DEX
	STX	QUOTEE
	JSR	PUTQUT

	LDA	#4
	STA	CURFAC
	LDX	I
	DEX
	JSR	DFACE
	LDY	#12
	JSR	DMESS

	LDA	#64
	STA	YPOS
	LDA	I
	STA	NAME
	JSR	WNDNAM
	LDA	#80
	STA	YPOS
	LDA	TOPIC
	STA	NAME
	JSR	WNDNAM

	DEC	TOPIC
	JSR	PUTARW
	INC	TOPIC

	LDA	#0
	STA	JABBER
	LDA	TALK
	STA	MOOD
	JSR	SHWMOD

	LDA	#0
	STA	CURFAC
	LDX	I
	DEX
	JSR	DFACE
	JSR	CLRMIS
	JMP	CLRPM2

;************************************
;	   SHOW MOOD
;************************************

SHWMOD	LDA	#1
	STA	CNTR2
	LDX	MOOD
	STA	FRMTMS,X


CNT2LP	LDA	#255
	STA	CNTR
CNTLOP	LDX	MOOD
	JSR	SWLOOP
	LDA	00020
	STA	PCOLR2
	LDY	#4
	JSR	YLOOP

	LDA	JABBER
	BEQ	LEAVIT
	LDX	I
	DEX
	LDA	RANDOM
	AND	#3
	STA	CURFAC
	JSR	DFACE

LEAVIT	DEC	CNTR
	BNE	CNTLOP
	DEC	CNTR2
	BNE	CNT2LP
	LDA	#0
	STA	CURFAC
	LDX	I
	DEX
	JMP	DFACE

;************************************
;	   I SAYS K THINKS LOVE ABOUT 
;	   TOPIC
;************************************
ISAYSK	LDA	#4
	STA	CURFAC
	LDX	K
	DEX

	STX	QUOTEE
	JSR	PUTQUT

	JSR	DFACE
	LDY	#13
	JSR	DMESS

	LDA	#64
	STA	YPOS
	LDA	I
	STA	NAME
	JSR	WNDNAM

	LDA	#73
	STA	YPOS
	LDA	K
	STA	NAME
	JSR	WNDNAM

	DEC	TOPIC
	JSR	PUTARW
	INC	TOPIC

	LDA	#1
	STA	JABBER
	LDA	LUV
	STA	MOOD
	JSR	STRTSD
	JSR	SHWMOD

	LDA	#1
	STA	JIFFYL
	LDA	#0
	STA	JIFFYH
	STA	JABBER
	STA	CURFAC
	LDX	K
	DEX
	JSR	DFACE
	JSR	CLRMIS
	JMP	CLRPM2

;         START SOUND

STRTSD	LDA	I
	EOR	#1
	AND	#1
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	STA	FSHIFT
	LDA	#1
	STA	MODE
	LDA	#0
	STA	DUR
	LDA	#255
	STA	JIFFYL
	LDA	#10
	STA	JIFFYH
	JMP	STRTIN

;************************************
;	   J SAYS TALK ABOUT TOPIC
;************************************

JSAYSA	LDA	I
	STA	TEMPI
	LDA	J
	STA	I
	JSR	ISAYSA
	LDA	TEMPI
	STA	I
	RTS

;************************************
;	   J SAYS K THINKS LOVE 
;	   ABOUT TOPIC
;************************************
JSAYSK	LDA	I
	STA	TEMPI
	LDA	J
	STA	I
	JSR	ISAYSK
	LDA	TEMPI
	STA	I
	RTS


;************************************
;	   J WONT SPEAK WITH YOU
;************************************
WNTSPK	LDY	#8
	JSR	DMESS
	LDA	#65
	STA	YPOS
	LDA	J
	STA	NAME
	JSR	WNDNAM
	LDA	#4
	STA	CURFAC
	LDX	J
	DEX
	JSR	DFACE
	LDA	#2
	STA	MOOD
	LDA	#0
	STA	JABBER
	JSR	SHWMOD
	LDX	J
	DEX
	JSR	LWRFON
	LDA	#0
	STA	CURFAC
	JMP	DFACE

;************************************
;	   DISPLAY TALKING
;************************************
DTALK	LDA	#100
	STA	MTHMVE
	JMP	CHFACE

MAINDL	DB	$70,$70,$30,199
	DW	MAINSR
	DB	7,7,135,132,$00,135,135,7,7,135,132,135,132,$17,65
	DW	MAINDL

MATRDL	DB	$70,$70,$70,$54
	DW	SCREEN
	DB	$14,$14,$30,4,4,4,4,4,4,4,4,4,4
	DB	4,4,4,4,4,4,4,4,4,4,4,65
	DW	MATRDL
;****    EQUATE FILE    ****

	ORG	$0080

;VARIABLES FROM LOGIC

I     	DS	1	;INDICES FOR
J     	DS	1	;THE 3-D 
K     	DS	1	;LOVE ARRAY
N     	DS	1	;NUM PLAYERS
TOPIC 	DS	1	;TALK ABOUT WHO?
TEMPST	DS	1	;THREE
TEMPMU	DS	1	;TEMPORARY
TEMP2 	DS	1	;VARIABLES
MAX   	DS	1
BOOST 	DS	1
AVAL  	DS	1
TALK  	DS	1
IDEX  	DS	1	;(I-1)*N
JDEX  	DS	1	;(J-1)*N
FATGUE	DS	1	;TALK OR NOT?
FLAG  	DS	1
FX    	DS	1	;DUMMYS FOR
FY    	DS	1	;FINDPS SUB
LX    	DS	1	;DUMMYS FOR
LY    	DS	1	;FINDLV SUB
LZ    	DS	1	;
PRODLO	DS	1	;HOLDS 2 BYTE 
PRODHI	DS	1	;RESULT OF *
MULTPR	DS	1	;OPERANDS FOR
DIVISR	DS	1	;* ROUTINE
RANK  	DS	1	;POSSIBLY UN-
GRANK 	DS	1	;NECESSARY
ATIMES	DS	2	;TIMES TALKED TO
LOVESH	DS	2	;ADDRESS OF LOVE
NUMARR	DS	1	;NUM CUPID ARROWS
NUMESP	DS	1	;NUM ESP OPTIONS
ARRNUM	DS	1	;ARROWS/TURN
TI    	DS	1	;OTHER 
TJ    	DS	1	;DUMMY
TDEX  	DS	1	;INDICES
NUMLOV	DS	1	;NUM LOVE YOU

;    VARS FOR MAIN CODE

MTHMVE	DS	1
UPDOWN	DS	1
DLYBYT	DS	1
NAME  	DS	1
XPOS  	DS	1	;RECORDS SCREEN
YPOS  	DS	1	;POSITION
CNTR  	DS	1	;  TWO
CNTR2 	DS	1	;COUNTERS
MOOD  	DS	1
JABBER	DS	1
TEMPI 	DS	1
VAR   	DS	1
QUOTEE	DS	1
GENDER	DS	1	;GENDER FLAG
LUV   	DS	1
CMPARE	DS	1
CURY  	DS	1
SAVEY 	DS	1
CRFACE	DS	1	;FACE DISPLAYED
BLINK 	DS	1
SOURCE	DS	1
TALKER	DS	1
CHOICE	DS	1
TURN  	DS	1	;NUM TURNS 
PACPOS	DS	1	;FLAG YOUR/OTHER
TMPHPS	DS	1

;    VARS FOR ALL

ARG1  	DS	1	;HIGH AND LOW
ARG1HI	DS	1	;    BYTES
ARG2  	DS	1	;OF ARGUMENTS
ARG2HI	DS	1	;    THAT
ARG3  	DS	1	;ARE USUALLY
ARG3HI	DS	1	;    USED
ARG4  	DS	1	;FOR SCREEN 
ARG4HI	DS	1	;    MAN-
ARG5  	DS	1	;IPULATION AND
ARG5HI	DS	1	;    THE
ARG6  	DS	1	;LIKE.
ARG6HI	DS	1	;

;    VARS FOR YAK3

FIRST 	DS	1
LAST  	DS	1
DUR   	DS	1
JIFFYL	DS	1
JIFFYH	DS	1
MODE  	DS	1
FSHIFT	DS	1
STATUS	DS	1
POINTR	DS	1
TEMP  	DS	1

;    MORE VARIABLES

TARG1 	DS	1
TARG1H	DS	1
TARG2 	DS	1
TARG2H	DS	1
CURFAC	DS	1
LSTCLL	DS	1
NBITS 	DS	1	;NUM BITS IN SQRT
ACCLO 	DS	1	;INPUTS FOR SQRT
ACCHI 	DS	1	;ROUTINE
SQRTA 	DS	1	;OUTPUT SQRT RTN
SELMSK	DS	1	;SELECT/START?
SETTMP	DS	1

;         VARS FOR COGITATE

MULTLO	DS	1	;2 BYTE OPERANDS
MULTHI	DS	1	;FOR THE 2 BYTE
DIVRLO	DS	1	;DIVIDE ROUTINE
DIVRHI	DS	1	;
TEMCTR	DS	1	;TEMP'RY COUNTER
QUOTNT	DS	1	;QUOTIENT 
TEMPX 	DS	1	;TEMPORARY X AND
TEMPY 	DS	1	;Y VALUES
STRSLO	DS	1	;FINAL STRESS IN
STRSHI	DS	1	;ONE PLANE
SUMLOW	DS	1	;STRESS FOR EACH
SUMHIH	DS	1	;ITERATION
DIRCTN	DS	1	;DIRECTION MOVED
NSQARE	DS	1	;N SQUARED
GEODIS	DS	1	;GEOMETRICAL DIS
STRCTR	DS	1	;NUM @ MIN STRESS
ZESTLO	DS	1	;STRESS WITHOUT
ZESTHI	DS	1	;ANY MOVEMENT
HAIRP1	DS	1	;VARIABLE HAIR 
HAIRP2	DS	1	;POSITIONS FOR
HAIRP3	DS	1	;THE HPOS'S
HAIRP4	DS	1	;
XCOUNT	DS	1	;COUNTER

;       POINTERS:SEE HARDWARE
;       MANUAL FOR EXPLANATION
DLPTR 	=	$0230
SDMCTL	=	$022F
CONSOL	=	$D01F
VCOUNT	=	$D40B
STICK0	=	$0278
TRIG0 	=	$D010
NMIEN 	=	$D40E
XITVBL	=	$E462
SKCTL 	=	$D20F
AUDCTL	=	$D208
AUDF1 	=	$D200
AUDC1 	=	$D201
AUDF2 	=	$D202
AUDC2 	=	$D203
AUDF3 	=	$D204
AUDC3 	=	$D205
AUDF4 	=	$D206
AUDC4 	=	$D207
ATTRAC	=	$004D
SIZEP0	=	$D008
SIZEP1	=	$D009
SIZEP2	=	$D00A
SIZEP3	=	$D00B
PCOLR0	=	$02C0
PCOLR1	=	$02C1
PCOLR2	=	$02C2
PCOLR3	=	$02C3
GRACTL	=	$D01D
GPRIOR	=	$026F
RANDOM	=	$D20A
HSCROL	=	$D404
CHBAS 	=	$02F4
COLOR0	=	$02C4
COLOR1	=	$02C5
COLOR2	=	$02C6
COLOR3	=	$02C7
COLOR4	=	$02C8
WSYNC 	=	$D40A
DRKMSK	=	$004E
COLRSH	=	$004F
DLVCTR	=	$0200
CHBASE	=	$D409
COLBK 	=	$D01A
COLPF0	=	$D016
COLPF1	=	$D017
COLPF2	=	$D018
COLPF3	=	$D019
HPOSP0	=	$D000
HPOSP1	=	$D001
HPOSP2	=	$D002
HPOSP3	=	$D003
HPOSM0	=	$D004
HPOSM1	=	$D005
HPOSM2	=	$D006
HPOSM3	=	$D007
COLPM0	=	$D012
CDTMV1	=	$0218
CDTMV2	=	$021A
CDTMA1	=	$0226
CDTMA2	=	$0228
CH    	=	$02FC
ORIGIN	=	$3000
;************************************
;*                                  *
;*   FINDING OUT WHO LOVES WHOM     *
;*                                  *
;* JUDGES CONTAIN THE NUMBERS OF    *
;* THE PLAYERS WHO DECIDE WHO TO    *
;* TAKE TO THE PROM. JUDGED ARE THE *
;* RETURNED ARE THE RANK TABLES     *
;* WHICH HOLD WHO THE JUDGES LOVE   *
;* JUDGES ARE ALWAYS 2,4,5,7        *
;* JUDGED ARE ALWAYS 1,3,6,8        *
;*                                  *
;************************************

FNDRNK	LDA	#1	;CALCULATE YOUR 
	STA	NUMLOV	;PERCEPTION FST
SCNDPS	LDX	#0	;
NEWEXS	LDA	JUDGES,X	;GET EACH OF THE
	CMP	N	;JUDGES. IF<=N
	BEQ	ONWARD	;THEN CARRY ON
	BCS	OUTWRD
ONWARD	STA	LX	;STORE JUDGE FOR
	STA	LZ	;LOVE CALC
	LDA	NUMLOV	;
	BEQ	NOTYOU	;IF YOUR VIEW THN
	STA	LZ	;LZ=1
NOTYOU	LDY	#0	;
	STY	TEMPX	;MAXLOV J FOR JD
NEWWHY	STY	TEMPY	;
	LDA	JUDGED,Y	;GET EACH OF THE
	CMP	N	;JUDGED. IF<=N
	BEQ	QUOVAD	;THEN CARRY ON
	BCS	NEXTEX	;OTHERWISE NEXT X
QUOVAD	STA	LY	;STORE JUDGED FOR
	STA	I	;LOVE CALCULATION
	JSR	FLOVE2	;LOVE CALC.
	CMP	TEMPX	;IF LOV<TX NEXT Y
	BCC	NEXTWY	;IF LOV>=TX;TX=LV
	STA	TEMPX	;
	LDA	NUMLOV	;IF YOUR VIEW PUT
	BEQ	NTUAGN	;JUDGED IN RNKTB1
	LDA	I	;
	STA	RNKTB1,X	;
	JMP	NEXTWY	;
NTUAGN	LDA	I	;IF ACTUAL PUT
	STA	RANKTB,X	;JUDGED IN RNKTAB
NEXTWY	LDY	TEMPY	;INC Y INDEX;IF>4
	INY		;THEN NEXT X
	CPY	#4	;
	BEQ	NEXTEX	;
	JMP	NEWWHY	;ELSE NEW Y
NEXTEX	INX		;INC X INDEX;IF>4
	CPX	#4	;THEN OUT
	BEQ	OUTWRD	;
	JMP	NEWEXS	;ELSE NEW X
OUTWRD	DEC	NUMLOV	;DEC VIEW FLAG &
	BEQ	SCNDPS	;IF 0 THEN SECOND
	RTS		;PASS

;************************************
;*                                  *
;* HEARTS SETS UP THE PLAYERS TO    *
;* INDICATE WHO IS TAKING WHOM TO   *
;* THE PROM. FOR BOTH PERCEIVED AND *
;* ACTUAL VALUES                    *
;*                                  *
;************************************

HEARTS	DB	$FF,$C3,$C3,$00,$00,$00,$00,$C3,$C3,$FF

HPOSTB	DB	$0,$6C,$0,$84,$0,$0,$A8,$0,$C0

KHEART	JSR	KREMOV	;CLEAR PLYER PRMS
	LDA	#PMBASE/256
	STA	$D407	;SET UP PMBASE
	LDA	#46	;AND THE NEC'RY
	STA	SDMCTL	;PARAMS
	LDA	#17
	STA	GPRIOR
	LDA	#2
	STA	GRACTL
	LDA	#2
	STA	SIZEP0	;NORMAL SIZE
	STA	SIZEP1
	STA	SIZEP2
	STA	SIZEP3
	LDA	TEMCTR	;PERCEIVED OR 
	BEQ	ACTRNK	;ACTUAL?
	LDA	N	;**PERCEIVED**
	CMP	#8	;SEE IF PLAYER 2
	BEQ	CALPL4	;& 3 ARE USED.IF
	LDX	#$FF	;SO THEN CALC THE
	STX	HPOSP3	;H-POSITION
	CMP	#6	;IF NOT THEN PUT 
	BCS	CALPL3	;THEM OFF SCREEN
	LDA	#$FF
	STA	HPOSP2
	STA	HPOSP3
	JMP	ROUND0
CALPL4	LDX	#3	;WHO DOES 3 LOVE?
	LDA	RNKTB1,X	;
	TAY		;STORE THE CORR-
	LDA	HPOSTB,Y	;ESPONDING H POS
	STA	HPOSP3	;IN HPOSP3
CALPL3	LDX	#2	;
	LDA	RNKTB1,X	;SIMILARLY FOR 
	TAY		;HPOSP2
	LDA	HPOSTB,Y
	STA	HPOSP2
ROUND0	LDX	#1	;HPOSP1 AND 0 ARE
	LDA	RNKTB1,X	;ALWAYS USED
	TAY
	LDA	HPOSTB,Y
	STA	HPOSP1
	DEX
	LDA	RNKTB1,X
	TAY
	LDA	HPOSTB,Y
	STA	HPOSP0
	JMP	DRWPLY	;JUMP TO DRAW
ACTRNK	LDA	N	;REPEAT ABOVE 
	CMP	#8	;PROCEDURE FOR
	BEQ	CALAC4	;ACTUAL VALUES
	LDX	#$FF
	STX	HPOSP3
	CMP	#6
	BCS	CALAC3
	LDA	#$FF
	STA	HPOSP2
	STA	HPOSP3
	JMP	ROUND1
CALAC4	LDX	#3
	LDA	RANKTB,X
	TAY
	LDA	HPOSTB,Y
	STA	HPOSP3
CALAC3	LDX	#2
	LDA	RANKTB,X
	TAY
	LDA	HPOSTB,Y
	STA	HPOSP2
ROUND1	LDX	#1
	LDA	RANKTB,X
	TAY
	LDA	HPOSTB,Y
	STA	HPOSP1
	DEX
	LDA	RANKTB,X
	TAY
	LDA	HPOSTB,Y
	STA	HPOSP0
DRWPLY	LDA	#$45	;DRAW THE PLAYERS
	STA	PCOLR0	;SET UP THE 
	STA	PCOLR1	;COLOURS
	STA	PCOLR2
	STA	PCOLR3

	LDY	#255	;CLEAR PLAYER
	LDA	#0	;RAM
KLRPM1	STA	PM0,Y
	STA	PM0+$100,Y
	DEY
	BNE	KLRPM1

	LDX	#PM0/256	;DRAW THE PLYRS
	STX	ARG1HI	;AT THE SPECIFIC
	STX	ARG2HI	;VERTICAL LOCATNS
	LDA	#$1D  	;ARG1 AND 2 HOLD
	STA	ARG1	;RAM POSITION
	LDA	#$B5
	STA	ARG2
	JSR	DRAWLP
	INC	ARG1HI
	INC	ARG2HI
	LDA	#$41
	STA	ARG1
	LDA	#$D9
	STA	ARG2
	JSR	DRAWLP
ENDRNK	RTS

KREMOV	LDA	#0	;SUBROUTINE TO 
	LDY	#8	;CLEAR PLAYER
KSETLT	STA	$D000,Y	;PARAMETERS
	DEY
	BPL	KSETLT
	RTS

DRAWLP	LDX	#9	;SUBROUTINE TO 
	LDY	#9	;PUT PLAYERS IN
REPLOP	LDA	HEARTS,X	;RAM
	STA	(ARG1),Y
	STA	(ARG2),Y
	DEX
	DEY
	BPL	REPLOP
	RTS
;***********************************
;         ENDING OF GOSSIP
; SELECT BETWEEN ACTUAL AND PERCEIVED
; ACTUAL    = LOVE(I,J,I)
; PERCEIVED = LOVE(I,J,1)
;***********************************
ENDING	JSR	CLSING	;CLOSING CREDITS
	JSR	PSCRN	;DRAW PERCEIVED
	LDA	#1	;SCREEN
	STA	TEMCTR	;TEMCTR=FLAG(ACT)
	JSR	KHEART	;PUT HTS ON SCRN
ELOOP	LDA	#PRMDAY/256
	STA	ARG4HI	;GET PERCEIVED
	LDA	#PRMDAY&255
	STA	ARG4	;MESSAGE ADDRESS
	JSR	DLINE	;PUT ON SCREEN
	LDA	#SPERCD&255
	STA	ARG4	;
	JSR	DNLINE	;PUT ON SCREEN

	JSR	DEBNCE	;DEBOUNCE KEY
	JSR	STEST4	;SELECT/START?
	JSR	SETACT	;GET NEWFACE VALS
	JSR	PSCRN2	;DRAW SCREEN
	DEC	TEMCTR	;DEC FLAG
	JSR	KHEART	;DRAW HTS
	LDA	#PRMDAY&255
	STA	ARG4	;GET ACTUAL MSGE
	JSR	DLINE	;ADDRS AND PUT UP
	LDA	#SACTUL&255
	STA	ARG4
	JSR	DNLINE

	JSR	DEBNCE	;DEBOUNCE KEYS
	JSR	STEST4	;SELECT/START?
	JSR	SETMAT	;NEW FACE VALUES
	JSR	PSCRN2	;DRAW SCREEN
	INC	TEMCTR	;INC FLAG
	JSR	KHEART	;DRAW HEARTS
	JMP	ELOOP	;JMP TO LOOP BEG

PRMDAY	DB	'PROM DAY!'
SPERCD	DB	'YOU THINK...'
SACTUL	DB	'..BUT REALLY'

DLINE	LDA	#SCRNHI	;SUBROUTINE TO 
	STA	ARG5HI	;PUT LINE ON MAT
	LDA	#SCRNLO+4
	STA	ARG5	;SCREEN
	LDY	#8
	JMP	PUTEXT

DNLINE	LDA	#SCRNLO+100
	STA	ARG5	;SUBROUTINE TO 
	LDY	#11	;PLACE LINE ON 
	JMP	PUTEXT	;MATRIX SCREEN

STEST2	LDA	CONSOL	;SUBROUTINE TO
	AND	#2	;TEST CONSOL FOR
	BNE	STEST2	;START OR SELECT 

STEST3	LDA	CONSOL	;OPTION
	AND	#2
	BNE	STEST3
	JMP	BEEP

STEST4	LDA	CONSOL
	AND	#1
	BEQ	STOVER

	LDA	CONSOL
	AND	#2
	BNE	STEST4

STEST5	LDA	CONSOL
	AND	#1
	BEQ	STOVER

	LDA	CONSOL
	AND	#2
	BNE	STEST5
	JMP	BEEP

STOVER	JMP	LOGIC	;IF START,RESTART GAME 

;***********************************
SETMAT	LDA	#1	;PACPOS=FLAG FOR
	STA	PACPOS	;PERCEIVED(=1)
	JMP	MATSET

SETACT	LDA	#0
	STA	PACPOS

MATSET	LDA	#2	;SET UP LOVE 
	STA	TI	;INDICATORS

ILUP11	LDA	#1
	STA	TJ

JLUP11	LDA	PACPOS
	BEQ	C714B0
	LDA	TJ
	ASL	A
	ASL	A
	ASL	A
	ADC	#1
	SEC
	SBC	#8
	STA	TDEX
	JMP	C7FAKE


C714B0	LDA	TJ
	ASL	A
	ASL	A
	ASL	A
	ADC	TI
	SEC
	SBC	#8
	STA	TDEX

C7FAKE ;IF TI=TJ THEN SELF FEELINGS
	LDA	TJ
	CMP	TI
	BNE	C720
	LDA	#$0A
	STA	PRODLO
	JMP	CSTORE

C720	;OUT$=LOVE(TI,TDEX)
	LDA	TI
	STA	LX
	LDA	TDEX
	STA	LY
	JSR	FNDLOV
	CMP	#UNKNWN
	BNE	CONVT2

	LDA	#$09
	STA	PRODLO
	JMP	CSTORE

CONVT2	JSR	DIVD29
	STA	PRODLO

CSTORE	LDA	TI
	STA	FX
	LDA	TJ
	STA	FY
	JSR	FINDPS
	LDA	PRODLO
	STA	FELNGS,Y

	INC	TJ
	LDA	N
	CMP	TJ
	BCS	JLUP11

	INC	TI
	LDA	N
	CMP	TI
	BCS	ILUP11
	RTS


;************************************
;         ENDING CREDITS FOR GOSSIP
;************************************
ENDTXT	DB	'                    PROM DAY!       PRESS SELECT TO SEE WHO IS TAKING YOU AND YOUR RIVALS TO'

	DB	' THE PROM !!!!!               ENJOY THE DANCE!!!!                        '

CLSING	LDA	#ENDTXT/256
	STA	ARG6HI
	LDA	#ENDTXT&255
	STA	ARG6
	LDA	#2
	STA	SELMSK

	JMP	CR9LUP

;	   DRAW SCREEN OF FACES
;
SCRNHI =	MATSCR/256
SCRNLO =	MATSCR&255

PSCRN	JSR	SETMAT

PSCRN2	JSR	STPCLK
	LDA	VCOUNT
	BNE	PSCRN2
	LDA	#2
	STA	HSCROL
	LDA	#AMODE4/256
	STA	CHBAS
;    COLORS
	LDA	#56
	STA	COLOR0
	LDA	#95
	STA	COLOR1
	LDA	#46
	STA	COLOR2
	LDA	PLYERS
	CLC
	ADC	PLYERS
	ADC	PLYERS
	STA	CMPARE

;************************************
;         CLEAR OUT SCREEN
;************************************
	LDA	#SCRNHI
	STA	ARG1HI
	LDA	#SCRNLO
	STA	ARG1
	LDX	#3
	LDY	#0
	TYA

CLRLUP	STA	(ARG1),Y
	INY
	BNE	CLRLUP
	INC	ARG1HI
	DEX
	BPL	CLRLUP

	LDY	#255
	LDA	#0

CLRIT2	STA	PM0,Y
	DEY
	BNE	CLRIT2
	JSR	RMOVPM

	LDA	#MATRDL&255
	STA	DLPTR
	LDA	#MATRDL/256
	STA	DLPTR+1

	LDA	#1
	JSR	DELAY2

	JSR	FILMAT
	JSR	PUTNME
	JSR	SETIME
	JMP	RMOVPM

;*************************************
;         FILL SCREEN WITH FACES
;*************************************
FILMAT	LDA	#SCRNLO+157
	STA	ARG1
	LDA	#SCRNHI
	STA	ARG1HI

	LDX	#1

FILLOP	JSR	PUTLNE
	LDA	ARG2
	STA	ARG1
	LDA	ARG2HI
	STA	ARG1HI
	INX
	CPX	PLYERS
	BNE	FILLOP
	RTS


;************************************
;         PUT LINE OF FACES
;************************************
PUTLNE	LDY	#0
	STY	CURY
PUTLOP	LDA	ARG1
	STA	ARG2
	LDA	ARG1HI
	STA	ARG2HI
	JSR	RNDFCE
	JSR	PUTFCE
	INC	CURY
	INY
	INY
	INY
	CPY	CMPARE
	BNE	PUTLOP
	RTS

;
;	  GET FACES
;
RNDFCE	STY	SAVEY
	TXA
	ASL	A
	ASL	A
	ASL	A
	ADC	CURY
	TAY
	LDA	FELNGS,Y
	ROL	A
	STA	CRFACE
	LDY	SAVEY
	RTS
;
;	  INC LINE
;
INCLNE	CLC
	LDA	ARG2
	ADC	#40
	STA	ARG2
	BCC	OUT
	INC	ARG2HI
OUT	RTS
;
;	  PUT FACE ONTO SCREEN
;
PUTFCE	LDA	CRFACE
	CLC
	ADC	#65
	JSR	PUTBYT
	LDA	CRFACE
	CLC
	ADC	#97
	JSR	PUTBYT
	LDA	CRFACE
	LSR	A
	CMP	#3
	BPL	GRT3
	LDA	#92
	JMP	CHIN

GRT3	CMP	#6
	BPL	GRT6
	LDA	#94
	JMP	CHIN
GRT6	CMP	#09
	BPL	NOCHIN
	LDA	#126
	JMP	CHIN
NOCHIN	JMP	INCLNE
CHIN	JMP	PUTBYT
;
;	  PUT 2 BYTES ON SCREEN
;
PUTBYT	STA	(ARG2),Y
	INY
	CLC
	ADC	#1
	STA	(ARG2),Y
	DEY
	JMP	INCLNE
;
;	  MOVE FACES
;
MASK  	DB	$55,$AA
ENDFRM	DB	5,2,1,0,0,1,1,1,1
CURFRM	DB	0,0,0,0,0,0,0,0,0
FRMDIR	DB	1,1,1,0,0,1,1,1,1
FRMTME	DB	8,21,23,17,19,57,35,37,41
FRMTMS	DB	1,1,1,1,1,1,1,1,1
FRAC	=	HIGH FTABLE

FACEHI	DB	FRAC,FRAC,FRAC,FRAC,FRAC+2,FRAC+2,FRAC+2,FRAC+2,FRAC+2

FACELO	DB	0,7*16,11*16,14*16,0,2*16,5*16,8*16,11*16
;
;	  SETUP COUNTDOWN TIMER
;
SETIME	LDA	#LOW SWFACE
	STA	CDTMA2
	LDA	#HIGH SWFACE
	STA	CDTMA2+1
	LDY	#1
	LDX	#0
	LDA	#2
	JMP	SETVBV
;
;	  SWAP FACE?
;
SWFACE	LDX	#8
SWMAIN	JSR	SWLOOP
	DEX
	BPL	SWMAIN
	JMP	SETIME



SWLOOP	DEC	FRMTMS,X
	BEQ	ZERO
	RTS
ZERO	LDA	FRMTME,X
	STA	FRMTMS,X
;
;	  CHOOSE FACE
;
	LDA	ENDFRM,X
	BEQ	PFNDLC ;IF NO CHANGE

;
CHFRME	LDA	CURFRM,X
	CLC
	ADC	FRMDIR,X
	STA	CURFRM,X
	BEQ	CHGDIR
	CMP	ENDFRM,X
	BNE	PFNDLC
;
;	  CHANGE DIR OF TRAVEL
;
CHGDIR	LDA	FRMDIR,X
	CMP	#255
	BEQ	CHGTO1
	LDA	#255
	STA	FRMDIR,X
	JMP	PFNDLC

CHGTO1	LDA	#1
	STA	FRMDIR,X
;
;	  FIND LOCATION OF FACE
;
PFNDLC	LDA	FACEHI,X
	STA	TARG1H
	LDA	FACELO,X
	LDY	CURFRM,X
	BEQ	STORE
	CLC

FLOOP	ADC	#16
	DEY
	BNE	FLOOP

STORE	STA	TARG1
;
;	  MOVE TO?
;
	LDA	#AMODE4/256+2
	STA	TARG2H
	TXA
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	ADC	#8
	STA	TARG2
;
;	  MOVE FACE INTO CHARSET
;
	JSR	MOVE
	INC	TARG1H
	INC	TARG2H
	JSR	MOVE

BIGFCE	LDA	#216
	STA	TARG2
	LDA	#MAINST/256+1  ;CHRSET+1
	STA	TARG2H
	JSR	MOVE16
	DEC	TARG1H
	DEC	TARG2H
	JMP	MOVE16

MOVE16	LDY	#15
MOVLOP	LDA	(TARG1),Y
	STA	(TARG2),Y
	DEY
	BPL	MOVLOP
	RTS


;
;	  MOVE 16 BYTES
;
MOVE	LDY	#15
MOVBYT	LDA	(TARG1),Y
	CPX	#3
	BPL	GRTHN6
	AND	MASK
	JMP	PUTBTE

GRTHN6	CPX	#6
	BPL	PUTBTE
	AND	MASK+1

PUTBTE	STA	(TARG2),Y
	DEY
	BPL	MOVBYT
	RTS

;***********************************
;         PUT NAMES DOWN SIDE
;***********************************
PUTNME	LDX	#1
	LDA	#SCRNHI
	STA	ARG1HI
	LDA	#SCRNLO+151
	STA	ARG1

OLOOP	LDA	GENDER
	BEQ	MALENM
	LDA	#NAMESG/256
	STA	ARG2HI
	LDA	#NAMESG&255
	STA	ARG2
	JMP	MULT04
MALENM	LDA	#NAMESB/256
	STA	ARG2HI
	LDA	#NAMESB&255
	STA	ARG2
MULT04	TXA
	ASL	A
	ASL	A
	ADC	ARG2
	STA	ARG2
	BCC	UNDER
	INC	ARG2HI
UNDER	LDY	#3
NLOOP	LDA	(ARG2),Y
	CLC
	SBC	#31
	STA	(ARG1),Y
	DEY
	BPL	NLOOP

	LDA	ARG1
	CLC
	ADC	#120
	STA	ARG1
	BCC	NOHI
	INC	ARG1HI
NOHI	INX
	CPX	PLYERS
	BNE	OLOOP

;************************************
;         DRAW NAMES ACROSS TOP
;************************************
;VERTNAME
	LDA	#SCRNHI
	STA	ARG1HI
	LDA	#SCRNLO+16
	STA	ARG1
	LDX	#0

ZLOOP	LDA	GENDER
	BEQ	MALEN1
	LDA	#NAMESG/256
	STA	ARG2HI
	LDA	#NAMESG&255
	STA	ARG2
	JMP	MULT14

MALEN1	LDA	#NAMESB/256
	STA	ARG2HI
	LDA	#NAMESB&255
	STA	ARG2
MULT14	TXA
	ASL	A
	ASL	A
	ADC	ARG2
	STA	ARG2
	BCC	NOPE
	INC	ARG2HI
NOPE	LDY	#1
CLOOP	LDA	(ARG2),Y
	CLC
	SBC	#31
	STA	(ARG1),Y
	LDA	ARG1
	CLC
	ADC	#47
	STA	ARG1
	INY
	CPY	#4
	BNE	CLOOP
	LDA	ARG1
	CLC
	SBC	#137
	STA	ARG1
	INX
	CPX	PLYERS
	BNE	ZLOOP
	LDA	TURN
	CMP	#10
	BEQ	ENDPMO

	LDY	#6
D2LOOP	LDA	STURN0,Y
	CLC
	SBC	#31
	STA	MATSCR+4,Y
	DEY
	BPL	D2LOOP

	LDY	#3
D1LOOP	LDA	STURN1,Y
	CLC
	SBC	#31
	STA	MATSCR+52,Y
	DEY
	BPL	D1LOOP

	LDA	#SCRNHI	;DISPLAY TURN 
	STA	ARG5HI
	LDA	#SCRNLO+57
	STA	ARG5
	LDY	#0
	LDA	#11
	SEC
	SBC	TURN
	JSR	DNUMBR
ENDPMO	RTS

STURN0	DB	'DAYS TO'
STURN1	DB	'PROM'
;     ***** GOSSIP LOGIC *****
	INCLUDE	D2:EQUATE.ASM
	ORG	ORIGIN
SCREEN	DS	$0400
	LIST	*
	INCLUDE	D2:AMODE4.BYT
	INCLUDE	D2:VOICES.BYT
PMSPCE	DS	$0300
	INCLUDE	D2:MAINST.BYT
	LIST	I
	INCLUDE	D2:DLYLST.ASM
	INCLUDE	D2:INSIDE.ASM
	INCLUDE	D2:INSID2.ASM
	INCLUDE	D2:INSID3.ASM
	INCLUDE	D2:TABLES.ASM
	INCLUDE	D2:COGTTE.ASM
	INCLUDE	D2:MAINSN.ASM
	INCLUDE	D2:MATRIX.ASM
	INCLUDE	D2:PLMOVE.ASM
	INCLUDE	D2:ENDING.ASM
	INCLUDE	D2:HEARTS.ASM
	INCLUDE	D2:FNDRNK.ASM
	INCLUDE	D2:CUPSHT.ASM
	INCLUDE	D2:ESPSPN.ASM

VTAB1 	=	VOICES
PMBASE	=	VOICES+$0400
PM0   	=	PMBASE+$0200
UNKNWN	=	0
JUDGES	DB	2,4,5,7
JUDGED	DB	1,3,6,8
SQRTLO	DS	256
SQRTHI	DS	256

LOGIC 	LDA	#$60
	STA	HAIRP1
	LDA	#$B0
	STA	HAIRP2
	LDA	#$30
	STA	HAIRP3
	LDA	#$80
	STA	HAIRP4
	JSR	STPLRS
	JSR	BEEP
	LDA	N
	STA	DIVISR
	STA	MULTPR
	JSR	MULTPY
	LDA	PRODLO
	STA	NSQARE
	LDA	N
	SEC
	SBC	#4
	STA	ARRNUM
	STA	NUMARR

;******** SQUARE ROOT TABLES *******
	LDA	#0
	STA	XCOUNT
KSQRLP	STA	MULTPR
	STA	DIVISR
	JSR	MULTPY
	LDX	XCOUNT
	LDA	PRODLO
	STA	SQRTLO,X
	LDA	PRODHI
	STA	SQRTHI,X
	INX
	STX	XCOUNT
	TXA
	BNE	KSQRLP

; ** LOVDIS CONVERSION **
	LDX	#$FF
	STX	LOVDIS
KIMLP1	LDA	#0
	CPX	#196
	BCC	EIGHT?
	LDA	#1
EIGHT?	STA	DIVRHI
	TXA
	CLC
	ADC	#60
	STA	DIVRLO
	STX	TEMCTR
	JSR	TWODIV
	LDX	TEMCTR
	LDA	PRODLO
	SEC
	SBC	#60
	STA	LOVDIS,X
	DEX
	BNE	KIMLP1

B20	LDA	#8
IFLOOP	STA	I
	STA	LX
	STA	FY

	LDA	#8
JFLOOP	STA	J
	STA	FX

	LDA	#8
KFLOOP	STA	K
	STA	LZ
	LDA	J
	STA	LY
	LDA	I
	CMP	J
	BEQ	KSLFLV
	CMP	K
	BEQ	KRANDM
	JSR	FLOVE2
	LDY	#0
	LDA	#0
	STA	(LOVESH),Y
	JMP	NEXTKF
KSLFLV	CMP	K
	BNE	NOTCEN
	JSR	FINDPS
	LDA	#128
	STA	XARRAY,Y
	STA	YARRAY,Y
NOTCEN	JSR	FLOVE2
	LDY	#0
	LDA	#$FF
	STA	(LOVESH),Y
	JMP	NEXTKF
KRANDM	LDX	RANDOM
	CPX	#$29
	BCC	KRANDM
	JSR	FLOVE2
	LDY	#0
	TXA
	JSR	KRNDOM

NEXTKF	DEC	K
	LDA	K
	BEQ	NEXTJF
	JMP	KFLOOP

NEXTJF	DEC	J
	LDA	J
	BEQ	NEXTIF
	JMP	JFLOOP

NEXTIF	DEC	I
	LDA	I
	BNE	IFLOOP
RNKLOP	JSR	FNDRNK
	LDA	#0
	STA	NUMLOV
	LDX	#3
RNKLP1	LDA	RANKTB,X
	CMP	#1
	BNE	NEXTRK
	INC	NUMLOV
NEXTRK	DEX
	BPL	RNKLP1
	LDA	NUMLOV
	CMP	#1
	BEQ	OKRANK
	JMP	B20

OKRANK	LDX	NSQARE
	LDA	#0
TIMELP	STA	TIMES,X
	DEX
	BNE	TIMELP
	LDX	N
TIM2LP	STA	TTIMES,X
	DEX
	BNE	TIM2LP

;**** MAIN LOOP--CREATE MEETINGS ****
;**** CLEAR DON'T CALL ARRAY     ****

B100	LDY	#8
	LDA	#0
DCLOOP	STA	DCL,Y
	DEY
	BPL	DCLOOP
	LDA	#1	;FOR I=1 TO N
	STA	I

;**** IF I=1 THEN INPUT J:G.120 ****

B107	LDA	I
	CMP	#1
	BNE	B110
	JSR	CHSWHM
	INC	J
	JMP	B120

B110	LDA	RANDOM	;J=IT(N*RND(0))+1
	AND	#7
	CMP	N
	BCS	B110
	STA	J
	INC	J

;**** IF J=I THEN 110****

B120	LDA	J
	CMP	I
	BEQ	B110

;**** DON'T CALL BACK ****

	LDY	I
	LDA	DCL,Y
	CMP	J
	BEQ	B110
	LDY	J
	LDA	DCL,Y
	CMP	#1
	BEQ	B130
	LDA	I
	STA	DCL,Y

; **** IF J>1 THEN 140 ****
B130	LDA	J
	CMP	#2
	BPL	B140

;****   ?I;"IS CALLING:G.200 ****

	JSR	ICLING
	JMP	B200

;**** IF GOSPY(J)+LOVE(J,I,J)/2 > RND(0)/2 THEN 200 ****

B140	LDA	J
	STA	LX
	STA	LZ
	LDA	I
	STA	LY
	JSR	FLOVE2
	LSR	A
	CLC
	ADC	#64
	LDY	J
	ADC	#8
	STA	TEMPST
	LDA	RANDOM
	LSR	A
	CMP	TEMPST
	BCC	B200

;**** IF I=1 THEN?J WILL NOT SPEAK WITH YOU:G.107 ****

	LDA	I
	CMP	#1
	BNE	B160
	JSR	WNTSPK
	JMP	B107

;**** LOVE(I,J,I)=LOVE(I,J,I)-(LOVE(I,J,I)+1)/10:G.107 ****

B160	JMP	B107

;**** DETERMINE THE TOPIC ****
;**** FATIGUE=0:FOR K=1 TO N:ATIMES(K)=0:NEXT K ****

B200	LDA	#0
	STA	FATGUE
	LDY	#1
FLOOP2	STA	(ATIMES),Y
	INY
	CPY	#9
	BNE	FLOOP2

;**** FLAG=0 ****

B205	LDA	#0
	STA	FLAG

;**** FOR K=1 TO N:IF ATIMES(K)=0 AND K<>I AND K<>J THEN FLAG=1 ****

	LDA	#1
	STA	K
KLOOP3	LDY	K
	LDA	(ATIMES),Y
	BNE	KNEXT3
	LDA	K
	CMP	I
	BEQ	KNEXT3
	CMP	J
	BEQ	KNEXT3
	LDA	#1
	STA	FLAG

;**** NEXT K:IF FLAG=0 THEN 500 ****
KNEXT3	INC	K
	LDA	N
	CMP	K
	BCS	KLOOP3
	LDA	FLAG
	BNE	B207
	JSR	MUSTGO
	JMP	B500

;**** IF I=1 OR J=1 THN GOS. 900 ****
;**** IF I>1 THEN 214 ****

B207	LDA	I
	CMP	#2
	BPL	B214

;**** INPUT TOPIC ****

	DEC	J
	JSR	CHSTPC
	INC	J
	INC	TOPIC

;**** G.250 ****

	JMP	B250

;**** MAX=0:TOPIC=0 ****

B214	LDA	#0
	STA	MAX
	STA	TOPIC

;**** FOR K=1 TO N:IF K=I OR ATIMES(K)=1 THEN 230 ****

	LDA	#1
	STA	K
KLOOP4	LDA	K
	CMP	I
	BNE	B216CT
	JMP	B230
B216CT	TAY
	LDA	(ATIMES),Y
	CMP	#1
	BNE	B218
	JMP	B230

;**** BOOST=0:IF K=J THN BOOST=32****
B218	LDA	#0
	STA	BOOST
	LDA	K
	CMP	J
	BNE	B220
	LDA	#32
	STA	BOOST

;**** AVAL=LOVE(I,K,I)+BOOST(0 OR 32)+RND(0)/4-TIMES(I,K)/(10+TTIMES(I))***

B220	LDA	I
	STA	LX
	STA	LZ
	LDA	K
	STA	LY
	JSR	FLOVE2
	CMP	#128
	BCS	GRETHN
	EOR	#$7F
	JMP	BOUT
GRETHN	AND	#$7F
BOUT	CLC
	ADC	BOOST
	STA	TEMPST
	LDA	RANDOM
	AND	#$0F
	CLC
	ADC	TEMPST

;**** -TIMES/TTIMES ****

	STA	AVAL

;**** IF AVAL>MAX THEN MAX=AVAL:TOPIC=K ****
	LDA	AVAL
	CMP	MAX
	BCC	B230
	STA	MAX
	LDA	K
	STA	TOPIC

;****NEXT K:IF J=1 THEN 280 ****

B230	INC	K
	LDA	N
	CMP	K
	BCC	B230CT
	JMP	KLOOP4
B230CT	LDA	J
	CMP	#1
	BEQ	B280

;**** IF GOSPY(J)+LOVE(J,I,J)/2>RND(0)/2+FATIGUE THEN 280 ****

B250	LDA	FATGUE
	BEQ	B280
	LDA	J
	STA	LX
	STA	LZ
	LDA	I
	STA	LY
	JSR	FLOVE2
	LSR	A
	CLC
	ADC	#64
	LDY	J
	ADC	#8
	STA	TEMPST
	LDA	RANDOM
	AND	#$0F
	CLC
	ADC	FATGUE
	CMP	TEMPST
	BCC	B280

;**** IF I=1 THEN ?"MUST GO NOW ****

	LDA	I
	CMP	#1
	BNE	B270
	JSR	MUSTGO

;**** GOTO 500 ****

B270	JMP	B500

;**** IF FATIGUE=0 OR I=1 OR GOSPY(I)+LOVE(I,J,I)/2>RND(0)+FATIGUE THEN 290 ****

B280	LDA	FATGUE
	BEQ	B290
	LDA	I
	CMP	#1
	BEQ	B290
	LDA	I
	STA	LX
	STA	LZ
	LDA	J
	STA	LY
	JSR	FLOVE2
	LSR	A
	CLC
	ADC	#64
	LDY	I
	CLC
	ADC	#8
	STA	TEMPST
	LDA	RANDOM
	LSR	A
	CLC
	ADC	FATGUE
	CMP	TEMPST
	BCC	B290

;**** IF J=1 THEN ?"MUST GO NOW ****

	LDA	J
	CMP	#1
	BNE	B286
	JSR	MUSTGO

;**** GOTO 500 ****

B286	JMP	B500

;**** TIMES(I,TOPIC)=TIMES(I,TPC)+1**

B290	LDA	I
	STA	FX
	LDA	TOPIC
	STA	FY
	JSR	FINDPS
	LDA	TIMES,Y
	CLC
	ADC	#1
	STA	TIMES,Y

;**** TTIMES(I)=TTIMES(I)+1:ATIMES(TOPIC)=1 ****

	LDY	I
	LDA	TTIMES,Y
	CLC
	ADC	#1
	STA	TTIMES,Y
	LDY	TOPIC
	LDA	#1
	STA	(ATIMES),Y

;**** ACTOR I TALKS ABOUT TOPIC ****
;**** IDEX=TOPIC*N-N+I:JDEX=TOPIC*N-N+J:IF TOPIC=J THEN 320 ****

	LDA	TOPIC
	ASL	A
	ASL	A
	ASL	A
	ADC	I
	SEC
	SBC	#8
	STA	IDEX

	LDA	TOPIC
	ASL	A
	ASL	A
	ASL	A
	ADC	J
	SEC
	SBC	#8
	STA	JDEX

	LDA	J
	CMP	TOPIC
	BNE	B303
	JMP	B320

;**** IF I>1 THEN TALK=LOVE(I,IDEX):G.307 ****

B303	LDA	I
	CMP	#1
	BCC	B304
	BEQ	B304
	LDA	I
	STA	LX
	LDA	IDEX
	STA	LY
	JSR	FNDLOV
	STA	TALK
	JMP	B307

;**** ?WHAT DO YOU SAY TO #J ABOUT TOPIC ****

B304	JSR	SHWFEL

;**** INPUT TALK ****

	LDA	TALK
	STA	MULTPR
	LDA	#31
	STA	DIVISR
	JSR	MULTPY
	LDA	PRODLO
	STA	TALK

;**** LOVE(I,JDEX)=TALK ****

B307	LDA	I
	STA	LX
	LDA	JDEX
	STA	LY
	JSR	FNDLOV
	LDA	TALK
	BNE	NOT01
	LDA	#1
NOT01	STA	(LOVESH),Y

;**** IF J=1 THEN ?I SAYS TALK ABOUT TOPIC ****

	LDA	J
	CMP	#1
	BNE	B320
	LDA	TALK
	JSR	DIVD29
	STA	TALK
	JSR	ISAYSA

;**** DUMP OUT THIRD-PERSON PERCEPTIONS ****

;**** IF J>1 THEN 330 ****

B320	LDA	J
	CMP	#2
	BPL	B330

;**** FOR K=1 TO N:IF K=I OR K=J OR K=TOPIC THEN 328 ****

	LDA	#1
	STA	K
KLOOP5	LDA	K
	CMP	I
	BEQ	B328
	CMP	J
	BEQ	B328
	CMP	TOPIC
	BEQ	B328

;**** IF LOVE(K,IDEX)=UNKNWN THEN 328 ****

	LDA	K
	STA	LX
	LDA	IDEX
	STA	LY
	JSR	FNDLOV
	CMP	#UNKNWN
	BEQ	B328

;**** ?#I SAYS #K THINKS LOVE(K,IDEX) ABOUT TOPIC ****

	LDA	K
	STA	LX
	LDA	IDEX
	STA	LY
	JSR	FNDLOV
	JSR	DIVD29
	STA	LUV
	JSR	ISAYSK


;**** NEXT K ****

B328	INC	K
	LDA	N
	CMP	K
	BCS	KLOOP5

;**** FOR K=1 TO N:IF K=I OR K=J OR K=TOPIC THEN 360 ****

B330	LDA	#1
	STA	K
KLOOP6	LDA	K
	CMP	I
	BEQ	JB360
	CMP	J
	BEQ	JB360
	CMP	TOPIC
	BEQ	JB360
	JMP	B337
JB360	JMP	B360


;**** IF LOVE(K,IDEX)=UNKNWN THEN A=LOVE(K,JDEX):G.B350 ****

B337	LDA	K
	STA	LX
	LDA	IDEX
	STA	LY
	JSR	FNDLOV
	CMP	#UNKNWN
	BNE	B338
	LDA	K
	STA	LX
	LDA	JDEX
	STA	LY
	JSR	FNDLOV
	STA	AVAL
	JMP	B350

;**** IF LOVE(K,JDEX)=UNKNWN THEN A=LOVE(K,IDEX):G.B350 ****

B338	LDA	K
	STA	LX
	LDA	JDEX
	STA	LY
	JSR	FNDLOV
	CMP	#UNKNWN
	BNE	B340
	LDA	K
	STA	LX
	LDA	IDEX
	STA	LY
	JSR	FNDLOV
	STA	AVAL
	JMP	B350

;**** AVAL=(LOVE(K,IDEX)+LOVE(K,JDEX))/2 ****

B340	LDA	K
	STA	LX
	LDA	IDEX
	STA	LY
	JSR	FNDLOV
	STA	TEMPST
	LDA	K
	STA	LX
	LDA	JDEX
	STA	LY
	JSR	FNDLOV
	STA	TEMP2
	CLC
	ADC	TEMPST
	ROR	A
	STA	AVAL

;**** IF LOVE(K,IDEX)=0 OR LOVE(K,JDEX)=0 THEN AVAL=2*AVAL ****

;**** LOVE(K,IDEX)=A:LOVE(K,JDEX)=A ****

B350	LDA	K
	STA	LX
	LDA	IDEX
	STA	LY
	JSR	FNDLOV
	LDA	AVAL
	STA	(LOVESH),Y

	LDA	K
	STA	LX
	LDA	JDEX
	STA	LY
	JSR	FNDLOV
	LDA	AVAL
	STA	(LOVESH),Y

;**** NEXT K ****

B360	INC	K
	LDA	N
	CMP	K
	BCC	B403
	JMP	KLOOP6

;**** NOW ACTOR J TALKS BACK ****
;**** IF TOPIC=J THEN TOPIC=I:IDEX=TOPIC*N-N+I:JDEX=TOPIC*N-N+J:G.426 ****

B403	LDA	TOPIC
	CMP	J
	BNE	B404
	LDA	I
	STA	TOPIC
	LDA	TOPIC
	ASL	A
	ASL	A
	ASL	A
	ADC	I
	SEC
	SBC	#8
	STA	IDEX

	LDA	TOPIC
	ASL	A
	ASL	A
	ASL	A
	ADC	J
	SEC
	SBC	#8
	STA	JDEX
	JMP	B426

;**** IF J>1 THEN 410 ****

B404	LDA	J
	CMP	#2
	BPL	B410

;**** ?YOUR RESPONSE:INPUT TALK ****

	JSR	RSPOND
	LDA	TALK
	STA	MULTPR
	LDA	#31
	STA	DIVISR
	JSR	MULTPY
	LDA	PRODLO
	STA	TALK

;**** G.415 ****

	JMP	B415

;**** TALK=LOVE(J,JDEX) ****

B410	LDA	J
	STA	LX
	LDA	JDEX
	STA	LY
	JSR	FNDLOV
	STA	TALK

;**** LOVE(J,IDEX)=TALK ****

B415	LDA	J
	STA	LX
	LDA	IDEX
	STA	LY
	JSR	FNDLOV
	LDA	TALK
	BNE	NOT02
	LDA	#1
NOT02	STA	(LOVESH),Y

;**** IF I=1 THEN ?J SAYS TALK ABOUT TOPIC ****

	LDA	I
	CMP	#1
	BNE	B426
	LDA	TALK
	JSR	DIVD29
	STA	TALK
	JSR	JSAYSA

;**** IF I>1 THEN 435 ****

B426	LDA	I
	CMP	#2
	BPL	B435

;**** FOR K=1 TO N:IF K=I OR K=J OR K=TOPIC THEN 432 ****

	LDA	#1
	STA	K
KLOOP7	LDA	K
	CMP	I
	BEQ	B432
	CMP	J
	BEQ	B432
	CMP	TOPIC
	BEQ	B432

;**** IF LOVE(K,JDEX)=UNKNWN THEN 432 ****
	LDA	K
	STA	LX
	LDA	JDEX
	STA	LY
	JSR	FNDLOV
	CMP	#UNKNWN
	BEQ	B432

;**** J SAYS K THINKS LOVE(K,JDEX) ABOUT TOPIC ****

	LDA	K
	STA	LX
	LDA	JDEX
	STA	LY
	JSR	FNDLOV
	JSR	DIVD29
	STA	LUV
	JSR	JSAYSK

;**** NEXT K ****

B432	INC	K
	LDA	N
	CMP	K
	BCS	KLOOP7

;**** FOR K=1 TO N:IF K=I OR K=J OR K=TOPIC THEN 460 ****

B435	LDA	#1
	STA	K
KLOOP8	LDA	K
	CMP	I
	BEQ	JB460
	CMP	J
	BEQ	JB460
	CMP	TOPIC
	BEQ	JB460
	JMP	B437
JB460	JMP	B460

;**** IF LOVE(K,IDEX)=UNKNWN THEN A=LOVE(K,JDEX):G.B450 ****

B437	LDA	K
	STA	LX
	LDA	IDEX
	STA	LY
	JSR	FNDLOV
	CMP	#UNKNWN
	BNE	B438
	LDA	K
	STA	LX
	LDA	JDEX
	STA	LY
	JSR	FNDLOV
	STA	AVAL
	JMP	B450

;**** IF LOVE(K,JDEX)=UNKNWN THEN A=LOVE(K,IDEX):G.B450 ****

B438	LDA	K
	STA	LX
	LDA	JDEX
	STA	LY
	JSR	FNDLOV
	CMP	#UNKNWN
	BNE	B440
	LDA	K
	STA	LX
	LDA	IDEX
	STA	LY
	JSR	FNDLOV
	STA	AVAL
	JMP	B450

;**** AVAL=(LOVE(K,IDEX)+LOVE(K,JDEX)/2 ****
B440	LDA	K
	STA	LX
	LDA	IDEX
	STA	LY
	JSR	FNDLOV
	STA	TEMPST

	LDA	K
	STA	LX
	LDA	JDEX
	STA	LY
	JSR	FNDLOV
	CLC
	ADC	TEMPST
	ROR	A
	STA	AVAL

;**** IF LOVE(K,IDEX)=0 OR LOVE(K,JDEX)=0 THEN A=2*A ****

;**** LOVE(K,IDEX)=A:LOVE(K,JDEX)=A ****
B450	LDA	K
	STA	LX
	LDA	IDEX
	STA	LY
	JSR	FNDLOV
	LDA	AVAL
	STA	(LOVESH),Y

	LDA	K
	STA	LX
	LDA	JDEX
	STA	LY
	JSR	FNDLOV
	LDA	AVAL
	STA	(LOVESH),Y

;**** NEXT K ****

B460	INC	K
	LDA	N
	CMP	K
	BCC	B470
	JMP	KLOOP8

;**** FATGUE=FATGUE+1:G.205 ****

B470	INC	FATGUE
	JMP	B205

;**** NEXT I ****

B500	INC	I
	LDA	N

	CMP	I
	BCC	B800
	JMP	B107

B800	JSR	MATRIX
	JSR	COG
	LDA	TURN
	CMP	#10
	BNE	J100
	JSR	FNDRNK
	JMP	ENDING

J100	JMP	B100
	END	LOGIC
;************************************
;         SHOW ESP EFFECT
;************************************

SPINMS	DB	13,12,13,16

DSPIN	LDA	#0
	STA	TALKER
	JSR	ERAOLD
	JSR	CLRPM3
	JSR	CLRPM2
	LDA	#0
	STA	SIZEP3
	LDA	TMPHPS
	CLC
	ADC	#2
	STA	HPOSP3

SESPLU	JSR	DESP
	JSR	SPINSO
	LDA	#5
	JSR	DELAY2
	INC	TALKER
	LDA	TALKER
	CMP	#31
	BNE	SESPLU
	JSR	BEEP
	LDA	#0
	STA	AUDC1
	JMP	CLRPM2


;***********************************
;         DRAW SWIRLS
;***********************************

SPIN
	DB	$00,$4C,$92,$90,$7C,$12,$92,$64
	DB	$00,$1C,$A0,$AC,$92,$6A,$0A,$70
	DB	$00,$70,$46,$2A,$92,$A8,$C4,$1C
	DB	$00,$64,$8A,$6A,$10,$AC,$A2,$4C

DESP  	LDA	#SPIN/256
	STA	ARG4HI
	LDA	TALKER
	AND	#3
	ASL	A
	ASL	A
	ASL	A
	CLC
	ADC	#SPIN&255
	BCC	DESPCO
	INC	ARG4HI

DESPCO	STA	ARG4
	LDA	#PMBASE/256+3
	STA	ARG3HI
	LDA	#122           ;WAS 121
	LDY	YPOS

ADEM5	CLC
	ADC	#12
	DEY
	BPL	ADEM5
	STA	ARG3

	LDY	#7
DP5LOP	LDA	(ARG4),Y
	STA	(ARG3),Y
	DEY
	BPL	DP5LOP
	RTS


;***********************************
;         SPINSOUND
;***********************************
SPINSO	LDA	TALKER
	AND	#3
	BEQ	SPCONT
	RTS

SPCONT	LDA	TALKER
	LSR	A
	LSR	A
	AND	#3
	TAY
	LDA	SPINMS,Y
	STA	AUDF1
	LDA	#170
	STA	AUDC1
	RTS

;*************************************
;         ERASE OLD FACE
;*************************************
ERAOLD	LDA	#SCRNHI-1
	STA	ARG1HI
	LDA	#SCRNLO+47
	STA	ARG1
	LDY	XPOS      ;MOVE TO RIGHT
	LDA	ARG1

ADEM6	CLC
	ADC	#3
	STA	ARG1
	BCC	NOHI6
	INC	ARG1HI

NOHI6	DEY
	BPL	ADEM6
	LDY	YPOS      ;MOVE DOWN
	LDA	ARG1

ADEM7	CLC
	ADC	#120
	STA	ARG1
	BCC	NOHI7
	INC	ARG1HI

NOHI7	DEY
	BPL	ADEM7

;         FILL IN BLOCK

	LDA	#0
	TAY
	STA	(ARG1),Y
	INY
	STA	(ARG1),Y
	LDY	#40
	STA	(ARG1),Y
	INY
	STA	(ARG1),Y
	LDY	#80
	STA	(ARG1),Y
	INY
	STA	(ARG1),Y
	RTS
;***********************************
;         FIRE ARROW
;***********************************
TWANG	DB	$04,$02,$FF,$02,$04

TWNGCL	DB	128,129,130,131,132,133,134,135,136,135,134,133,132,131,130,129,128

DSHOOT	LDA	#4
	STA	GPRIOR
	JSR	CLRPM2
	LDA	#1
	STA	SIZEP2
	LDA	#46
	STA	PCOLR2
	LDA	#40
	STA	PACPOS

	LDA	#200
	STA	AUDF1
	LDA	#170
	STA	AUDC1

PACMLP	LDA	PACPOS
	STA	HPOSP2
	LDA	#1
	JSR	DELAY2
	JSR	DPAC

;         SOUND

	LDA	PACPOS
	CMP	#60
	BPL	SNDOFF
	SEC
	SBC	#39
	LSR	A
	TAY
	LDA	#5
	STA	AUDF1
	LDA	TWNGCL,Y
	STA	AUDC1
	JMP	SNCSND

SNDOFF	LDA	#0
	STA	AUDC1

SNCSND	INC	PACPOS
	LDA	PACPOS
	CLC
	ADC	#6
	CMP	TMPHPS
	BNE	PACMLP
	JSR	CLRPM2
	JSR	CLRPM3

	LDA	#250
	STA	AUDF1
	LDA	#174
	STA	AUDC1
	LDA	#200
	STA	AUDF2
	LDA	#142
	STA	AUDC2
	LDY	#10
	JSR	YLOOP
	LDA	#0
	STA	AUDC1
	STA	AUDC2
	RTS

;***********************************
;         DRAW FLYING ARROW
;***********************************

DPAC	LDA	#PMBASE/256+3
	STA	ARG3HI

	LDA	#0
	LDY	YPOS
	SEC
	SBC	#2

ADEM3	CLC
	ADC	#12
	DEY
	BPL	ADEM3
	ADC	#254
	STA	ARG3
	LDY	#4

DPLOOP	LDA	TWANG,Y
	STA	(ARG3),Y
	DEY
	BPL	DPLOOP
	RTS

;************************************
;         DELAY2
;************************************

DELAY2	CLC
	ADC	00020
DEL2LP	CMP	00020
	BNE	DEL2LP
	RTS
SOUND	DB $AF,$A4,$AF,$A4,$AF,$A4,$AF,$A4
	DB $AF,$A4,$AF,$A4,$AF,$A4,$AF,$A4
	DB $AF,$A4,$AF,$A4,$AF,$A4,$AF,$A4
	DB $AF,$A4,$AF,$A4,$AF,$A4,$AF,$A4
	DB $AF,$A4,$AF,$A4,$AF,$AC,$AE,$AA
	DB $AB,$A8,$AA,$A6,$A9,$A4,$A6,$A2
	DB $A3,$A0

;**** SUBROUTINE TO RING PHONE ****

RNGFON	LDA	#32
	STA	AUDF1
	LDA	#42
	STA	AUDF2
	LDA	#0
	STA	UPDOWN

MVUPDN	LDY	UPDOWN
	LDA	SOUND,Y
	STA	AUDC1
	STA	AUDC2
	JSR	DFNRNG
	JSR	DELAY
	JSR	DFONDN
	JSR	DELAY
	INC	UPDOWN
	LDA	UPDOWN
	CMP	#50
	BNE	MVUPDN
	LDY	#255
	JMP	YLOOP

;**** RAISE PHONE TO HEAD ****

RSFONE	JSR	DFONUP
	JSR	STARG1
	LDA	HEDLOC,X
	CLC
	ADC	ARG1
	ADC	#22
	STA	ARG1
	LDA	#201
	LDY	#0
	STA	(ARG1),Y
	LDY	#40
	LDA	(ARG1),Y
	LDY	#20
	STA	(ARG1),Y
	RTS

;**** LOWER PHONE TO REST ****

LWRFON	JSR	DFONDN
	JSR	STARG1
	LDA	HEDLOC,X
	CLC
	ADC	ARG1
	ADC	#22
	STA	ARG1
	LDA	#0
	TAY
	STA	(ARG1),Y
	LDY	#17
	LDA	(ARG1),Y
	CLC
	ADC	#1
	LDY	#20
	STA	(ARG1),Y
	RTS

;**** TIME DELAYS ****

DELAY	LDY	#7
YLOOP	LDA	#255
	STA	DLYBYT
BLOOP	LDA	CONSOL
	CMP	#7
	BNE	DEXIT
	DEC	DLYBYT
	BNE	BLOOP
	DEY
	BNE	YLOOP
DEXIT	RTS

;**** DRAW MAIN SCREEN ****

MAIN	LDA	#0
	STA	SDMCTL  ;TURNOFF DISPLAY
	JSR	STPCLK
	LDA	#MAINDL&255
	STA	DLPTR	;SET UP DISPLAY
	LDA	#MAINDL/256
	STA	DLPTR+1	;LIST
	LDA	#40	;SET UP COLOURS
	STA	COLOR0
	LDA	#94
	STA	COLOR1
	LDA	#148
	STA	COLOR2
	LDA	#15
	STA	COLOR3
	STA	HSCROL	;INITIALSE SCROLL
	LDA	#113
	STA	COLOR4
	LDA	#MAINST/256
	STA	CHBAS	;INSERT CHAR SET
	JSR	CLRMIS	;CLEAR MISSILES

CLRSCN	JSR	STARG1	;CLEAR SCREEN
	LDY	#0
	TYA
	LDX	#1

CLRLOP	STA	(ARG1),Y
	INC	ARG1
	BNE	CLRLOP
	INC	ARG1HI
	DEX
	BPL	CLRLOP

DLINAB	LDA	#1	;ENABLE DLI
	JSR	DELAY2
	LDA	#FDLI/256
	STA	DLVCTR+1
	LDA	#FDLI&255
	STA	DLVCTR
	LDA	#192
	STA	NMIEN

PUTBOD	LDX	#0	;PUT PLYS ON THE
	TXA		;SCREEN:X INC'D
	STA	CURFAC	;TO N
JUMP	JSR	PUTALL
	INX
	CPX	PLYERS
	BNE	JUMP
	JSR	DHAIR	;PUT HAIR ON PLYS
	JMP	DGNDER	; " YOU IF FEMALE

PUTALL	JSR	DFACE	;PUT FACES UP
	JSR	DBODY	;PUT BODYS UP
	JSR	DFONDN	
	JSR	DNAME	;PUT NAMES UP
	LDA	#46
	STA	SDMCTL
ENDPUT	RTS

;**** SUBROUTINE TO PUT FACES ON ****
;****           SCREEN           ****

DFACE	JSR	STARG1
	LDA	HEDLOC,X
	ADC	ARG1
	STA	ARG1

DRAWTP	LDY	CURFAC
	LDA	FACETP,Y
	LDY	#0
	STA	(ARG1),Y
	INY
	CLC
	ADC	#1
	STA	(ARG1),Y

DRWBOT	LDA	ARG1
	CLC
	ADC	#20
	STA	ARG1
	LDY	CURFAC
	LDA	FACEBT,Y
	LDY	#0
	STA	(ARG1),Y
	INY
	CLC
	ADC	#1
	STA	(ARG1),Y
	RTS

;**** PUT BODY CHARSET ON SCREEN ****

DBODY	JSR	STARG1
	LDA	HEDLOC,X
	ADC	ARG1
	ADC	#39
	STA	ARG1
	LDA	GENDER	;TEST FOR GENDER
	BEQ	BOYBOD	;SINCE THIS 
	LDA	#BODCH1&255
	STA	ARG2	;CHANGES ORDER OF
	LDA	#BODCH1/256
	STA	ARG2HI	;THE PLAYERS
	JMP	AFTBOY	;MALE CHARSET IS
BOYBOD	LDA	#BODCHR&255
	STA	ARG2	;BODCHR AND THE
	LDA	#BODCHR/256
	STA	ARG2HI	;FEMALE IS BODCH1
AFTBOY	TXA
	ASL	A
	ASL	A
	ASL	A
	ADC	ARG2
	STA	ARG2
	BCC	DRWBOD
	INC	ARG2HI

DRWBOD	LDY	#0	;SET BOD CHARS ON
BODTOP	LDA	(ARG2),Y
	STA	(ARG1),Y
	INY		;SCREEN
	CPY	#4
	BNE	BODTOP
	LDA	ARG1
	ADC	#15
	STA	ARG1

BODBOT	LDA	(ARG2),Y
	STA	(ARG1),Y
	INY
	CPY	#8
	BNE	BODBOT
	RTS

;**** PUTS THE SCREEN ADDRESS IN ****
;**** ARG1 LOW AND HIGH BYTE     ****

STARG1	LDA	#MAINSR&255
	STA	ARG1
	LDA	#MAINSR/256
	STA	ARG1HI
	CLC
	RTS

;**** FINDS LOCATION OF PHONE ON ****
;**** SCREEN                     ****

FNDFON	JSR	STARG1
	LDA	HEDLOC,X
	ADC	ARG1
	ADC	#121
	STA	ARG1
	BCC	NOHICY
	INC	ARG1HI
NOHICY	RTS

DFONDN	JSR	FNDFON
	LDA	#193
	LDY	#0
	STA	(ARG1),Y
	LDA	#194
	INY
	STA	(ARG1),Y
	CPX	#4
	BPL	FONBAS
	LDA	#199
	LDY	#20
	STA	(ARG1),Y
	LDA	#200
	INY
	STA	(ARG1),Y
	RTS

FONBOT	DB	0,0,0,0,23,28,33,38

FONBAS	LDA	FONBOT,X
	TAY
	LDA	#187
	STA	(ARG1),Y
	INY
	STA	(ARG1),Y
	INY
	LDA	#188
	STA	(ARG1),Y
	RTS

DFNRNG	JSR	FNDFON
	LDA	#195
	LDY	#0
	STA	(ARG1),Y
	LDA	#196
	INY
	STA	(ARG1),Y
	RTS

DFONUP	JSR	FNDFON
	LDA	#197
	LDY	#0
	STA	(ARG1),Y
	LDA	#198
	INY
	STA	(ARG1),Y
	RTS

;**** PUT NAMES UNDER PLAYERS ****

DNAME	JSR	STARG1
	LDA	NAMLOC,X
	ADC	ARG1
	STA	ARG1
	BCC	CONT
	INC	ARG1HI

CONT	LDA	GENDER	;GENDER CHANGES
	BEQ	MALEGN	;THE ORDER OF
	LDA	#NAMESG&255
	STA	ARG2	;THE NAMES
	LDA	#NAMESG/256
	STA	ARG2HI	;NAMESG GIRL FST
	JMP	CONTKI	;NAMESB BOY FST
MALEGN	LDA	#NAMESB&255
	STA	ARG2
	LDA	#NAMESB/256
	STA	ARG2HI
CONTKI	TXA
	ASL	A
	ASL	A
	ADC	ARG2
	STA	ARG2
	BCC	NOCARY
	INC	ARG2HI

NOCARY	LDY	#3
NAMLOP	LDA	(ARG2),Y
	CLC
	SBC	#31
	STA	(ARG1),Y
	DEY
	BPL	NAMLOP
ENDNAM	RTS

;**** HAIR ROUTINES AND DATA ****

HAIR	DB $3C,$3C,$7E,$66,$66,$42,$42,$42,$42,$42,$42,$42,$42,$C3,$81,$81

HAIR2	DB $3C,$3C,$7E,$7E,$7E,$76,$62,$62,$42,$42,$42,$42,$42,$42,$81,$81

;**** PLYER ROUTINE FOR THE HAIR ****

DHAIR	JSR	RMOVPM
	LDA	#PMBASE/256
	STA	$D407
	LDA	#46
	STA	SDMCTL
	LDA	#17
	STA	GPRIOR
	LDA	#3
	STA	GRACTL
	STA	SIZEP0
	STA	SIZEP1
	LDA	HAIRP2
	STA	HPOSP0
	LDA	HAIRP1
	STA	HPOSP1
	LDA	#37
	STA	PCOLR0
	LDA	#53
	STA	PCOLR1

	LDY	#255
	LDA	#0

CLRPM	STA	PM0,Y
	DEY
	BNE	CLRPM

PHAIR	LDA	#PM0/256
	STA	ARG1HI
	STA	ARG2HI

	LDY	#15
	LDA	#12
	STA	ARG1
	LDA	#140
	STA	ARG2

HLOOP1	LDA	HAIR,Y
	STA	(ARG1),Y
	STA	(ARG2),Y
	DEY
	BPL	HLOOP1
	LDA	GENDER	;GENDER AGAIN 
	BEQ	FIVSEV	;CHANGES WHERE 
	LDA	#6	;THE HAIR GOES
	STA	ZESTLO	;IF GENDER=1
	LDA	#8	;GIRL FIRST
	STA	ZESTHI	;IF GENDER=0
	JMP	COMPHR	;BOY FIRST
FIVSEV	LDA	#5
	STA	ZESTLO
	LDA	#7
	STA	ZESTHI
COMPHR	LDA	PLYERS
	CMP	ZESTLO
	BMI	HAIRDN
	LDY	#12
	LDA	#184
	STA	ARG1

HLOOP3	LDA	HAIR2,Y
	STA	(ARG1),Y
	DEY
	BPL	HLOOP3

	LDA	PLYERS
	CMP	ZESTHI
	BMI	HAIRDN
	LDY	#15
	LDA	#56
	STA	ARG1

HLOOP4	LDA	HAIR2,Y
	STA	(ARG1),Y
	DEY
	BPL	HLOOP4

HAIRDN	RTS

RMOVPM	LDA	#0
	LDY	#8
SETLFT	STA	$D000,Y
	DEY
	BPL	SETLFT
	RTS

;**** SETUP CLOCK TO BLINK EYES ****

BLNKYS	LDA	#LOW DBLINK
	STA	CDTMA2
	LDA	#HIGH DBLINK
	STA	CDTMA2+1

	LDA	RANDOM
	AND	#$1F
	ADC	#1
	STA	CDTMV2
	LDA	#0
	STA	CDTMV2+1
	RTS

DBLINK	LDX	RANDOM
	CPX	PLYERS
	BCS	DBLINK
	STX	BLINK
	LDA	RANDOM
	AND	#3
	STA	CURFAC
	JSR	PUTEYE


	LDA	#LOW BLRSET
	STA	CDTMA2
	LDA	#HIGH BLRSET
	STA	CDTMA2+1
	LDA	#2
	STA	CDTMV2
	LDA	#0
	STA	CDTMV2+1
	RTS


BLRSET	LDX	BLINK
	LDA	#0
	STA	CURFAC
	JSR	PUTEYE
	JMP	BLNKYS

PUTEYE	LDA	#MAINSR&255
	STA	ARG3
	LDA	#MAINSR/256
	STA	ARG3HI

	LDA	HEDLOC,X
	CLC
	ADC	ARG3
	STA	ARG3

DEYES	LDY	CURFAC
	LDA	FACETP,Y
	LDY	#0
	STA	(ARG3),Y
	INY
	CLC
	ADC	#1
	STA	(ARG3),Y
	RTS

;**** STOP CLOCK ****

STPCLK	LDA	#0
	STA	CDTMV2
	STA	CDTMV2+1
	RTS
;************************************
;         DISPLAY MATRIX SCREEN
;************************************

MATRIX	JSR	PSCRN
	JSR	DOWHAT
	INC	TURN
	JMP	MAIN
;************************************
;         DO WHAT??
;************************************
SSELCT	DB	'RETURN '
SSHOOT	DB	'SHOOT  '
SARROW	DB	'ARROWS '
SUSESP	DB	'USE ESP'
SESP  	DB	'ESP    '

DOWHAT	LDA	TRIG0
	BEQ	DOWHAT
	LDA	#255
	STA	CH
	LDA	#2
	JSR	DELAY2
	LDA	CH
	CMP	#255
	BNE	DOWHAT
	LDA	#0
	STA	CHOICE
	JSR	PUTNUM
	JSR	DRASEL
	LDA	#3
	STA	SIZEP0
	LDA	#8
	STA	PCOLR0
	LDA	#53
	STA	HPOSP0
	LDA	#4
	STA	GPRIOR
	JSR	VCURS

	LDY	#50
	JSR	YLOOP

E2EXIT	JSR	CHGCHC
	LDA	CH
	CMP	#12
	BEQ	RET4
	LDA	TRIG0
	BNE	E2EXIT

RET4	LDA	#20
	STA	PCOLR0
	JSR	BEEP

	LDA	CHOICE
	CMP	#1
	BEQ	P1
	CMP	#2
	BEQ	P2

P0	RTS
P1	JSR	SELMAT
	JSR	FIRARW
	DEC	NUMARR
	JMP	DOWHAT
P2	JSR	SELMAT
	JSR	DSPIN
	JSR	SHWESP
	DEC	NUMESP
	JMP	DOWHAT

CHGCHC	LDA	CH
	CMP	#14
	BEQ	UP4

	LDA	STICK0
	CMP	#14
	BNE	TSTDWN
UP4	DEC	CHOICE
	LDA	CHOICE	;MOVE UP LIST
	CMP	#$FF
	BNE	TSTESP
	LDA	#2
	STA	CHOICE

TSTESP	LDA	CHOICE
	CMP	#2
	BNE	TSTARW
	LDA	NUMESP
	BNE	TSTARW
	LDA	#1
	STA	CHOICE

TSTARW	LDA	CHOICE
	CMP	#1
	BNE	TSTOUT
	LDA	NUMARR
	BNE	TSTOUT
	STA	CHOICE
TSTOUT	JMP	VCURS

TSTDWN	LDA	CH
	CMP	#15
	BEQ	TDCONT

	LDA	STICK0
	CMP	#13
	BEQ	TDCONT
	RTS

TDCONT	INC	CHOICE
	LDA	CHOICE
	CMP	#3
	BNE	TST2AR
	STA	CHOICE
	JMP	TST2OT

TST2AR	LDA	CHOICE
	CMP	#1
	BNE	TS2ESP
	LDA	NUMARR
	BNE	TS2ESP
	LDA	#2
	STA	CHOICE

TS2ESP	LDA	CHOICE
	CMP	#2
	BNE	TST2OT
	LDA	NUMESP
	BNE	TST2OT
	STA	CHOICE

TST2OT	JMP	VCURS
DRASEL	LDA	#SSELCT/256
	STA	ARG4HI
	LDA	#SSELCT&255
	STA	ARG4

	LDA	#SCRNHI+1
	STA	ARG5HI
	LDA	#SCRNLO+208
	STA	ARG5
	LDY	#5
	JSR	PUTEXT

	LDA	#SSHOOT/256
	STA	ARG4HI
	LDA	#SSHOOT&255
	STA	ARG4

	LDA	#SCRNHI+2
	STA	ARG5HI
	LDA	#SCRNLO+32
	STA	ARG5

	LDY	#5
	JSR	PUTEXT

	LDA	#SARROW/256
	STA	ARG4HI
	LDA	#SARROW&255
	STA	ARG4

	LDA	#SCRNLO+32+40
	STA	ARG5
	LDA	#SCRNHI+2
	STA	ARG5HI
	LDY	#5
	JSR	PUTEXT

	LDA	#SUSESP/256
	STA	ARG4HI
	LDA	#SUSESP&255
	STA	ARG4

	LDA	#SCRNHI+2
	STA	ARG5HI
	LDA	#SCRNLO+32+120
	STA	ARG5

	LDY	#6
	JMP	PUTEXT

;***********************************
;         PUT # OF ESP/ARROWS
;***********************************
PUTNUM	LDA	#SARROW/256
	STA	ARG4HI
	LDA	#SARROW&255
	STA	ARG4

	LDA	#SCRNHI+1
	STA	ARG5HI
	LDA	#SCRNLO+8
	STA	ARG5

	LDY	#5
	JSR	PUTEXT

	LDA	#SCRNHI+1
	STA	ARG5HI
	LDA	#SCRNLO+8+40
	STA	ARG5
	LDY	#0
	LDA	NUMARR
	JSR	DNUMBR

;         PUT # OF ESP

	LDA	#SESP/256
	STA	ARG4HI
	LDA	#SESP&255
	STA	ARG4

	LDA	#SCRNHI+1
	STA	ARG5HI
	LDA	#SCRNLO+8+80
	STA	ARG5

	LDY	#3
	JSR	PUTEXT

	LDA	#SCRNHI+1
	STA	ARG5HI
	LDA	#SCRNLO+8+120
	STA	ARG5
	LDY	#0
	LDA	NUMESP
	JMP	DNUMBR

;************************************
;     MOVE TEXT FROM ARG4 TO ARG5
;        DISPLAY Y CHARACTERS
;************************************

PUTEXT	LDA	(ARG4),Y
	CLC
	SBC	#31
	STA	(ARG5),Y
	DEY
	BPL	PUTEXT
	RTS

;************************************
;         CLEAR PM0
;************************************
CLRPM0	LDA	#PMBASE/256+2
	STA	ARG5HI
	LDA	#0
	STA	ARG5
	LDY	#127
CLRIT	STA	(ARG5),Y
	DEY
	BNE	CLRIT
	RTS

;************************************
;         VERTICAL CURSOR
;************************************
VCURS	LDA	#255
	STA	CH
	JSR	CLRPM0
	LDA	CHOICE
	CMP	#1
	BEQ	CH1
	CMP	#2
	BEQ	CH2

CH0	LDA	#58
	JMP	DBACK
CH1	LDA	#68
	JMP	DBACK
CH2	LDA	#78

DBACK	STA	ARG5
	LDA	#PMBASE/256+2
	STA	ARG5HI
	LDA	#255
	LDY	#10
DPCURS	STA	(ARG5),Y
	DEY
	BNE	DPCURS

	JSR	BEEP
	LDY	#80
	JMP	YLOOP

;************************************
;         SELECT LOC ON MATRIX
;************************************
SELMAT	LDA	#1
	STA	XPOS
	LDA	#2
	STA	YPOS
	JSR	PUTCUR

	LDY	#50
	JSR	YLOOP

SBNCE	LDA	#255
	STA	CH
	LDA	#2
	JSR	DELAY2
	LDA	CH
	CMP	#255
	BNE	SBNCE

	LDA	TRIG0
	BEQ	SBNCE

TSTRGT	LDA	CH
	CMP	#7
	BEQ	RIGHT4

	LDA	STICK0
	CMP	#7
	BNE	TSTLFT
RIGHT4	LDA	XPOS
	CMP	PLYERS
	BEQ	ROLL1
	INC	XPOS

	LDA	XPOS
	CMP	YPOS
	BEQ	TRMATR
	JMP	TEXIT
TRMATR	CMP	PLYERS
	BEQ	ROLL1

	INC	XPOS
	JMP	TEXIT

ROLL1	LDA	#1
	STA	XPOS
	JMP	TEXIT

TSTLFT	LDA	CH
	CMP	#6
	BEQ	LEFT5

	LDA	STICK0
	CMP	#11
	BNE	TESTDN
LEFT5	LDA	XPOS
	CMP	#1
	BEQ	ROLL2
	DEC	XPOS

	LDA	XPOS
	CMP	YPOS
	BEQ	TLCONT
	JMP	TEXIT
TLCONT	CMP	#1
	BEQ	ROLL2
	DEC	XPOS
	JMP	TEXIT

ROLL2	LDA	PLYERS
	CMP	YPOS
	BNE	RL2CNT
	SEC
	SBC	#1
RL2CNT	STA	XPOS
	JMP	TEXIT

TESTDN	LDA	CH
	CMP	#15
	BEQ	DOWN5

	LDA	STICK0
	CMP	#13
	BNE	TESTUP
DOWN5	LDA	YPOS
	CMP	PLYERS
	BEQ	ROLL3
	INC	YPOS

	LDA	YPOS
	CMP	XPOS
	BNE	TEXIT
	CMP	PLYERS
	BEQ	ROLL3
	INC	YPOS
	JMP	TEXIT

ROLL3	LDA	XPOS
	CMP	#2
	BNE	RL3CNT
	LDA	#3
	STA	YPOS
	JMP	TEXIT

RL3CNT	LDA	#2
	STA	YPOS
	JMP	TEXIT

TESTUP	LDA	CH
	CMP	#14
	BEQ	RIGHT5

	LDA	STICK0
	CMP	#14
	BNE	TRGTST
RIGHT5	LDA	YPOS
	CMP	#2
	BEQ	ROLL4
	DEC	YPOS

	LDA	YPOS
	CMP	XPOS
	BNE	TEXIT
	CMP	#2
	BEQ	ROLL4
	DEC	YPOS
	JMP	TEXIT

ROLL4	LDA	XPOS
	CMP	PLYERS
	BNE	RL4CNT
	LDA	PLYERS
	SEC
	SBC	#1
	STA	YPOS
	JMP	TEXIT
RL4CNT	LDA	PLYERS
	STA	YPOS
	JMP	TEXIT

TRGTST	LDA	STICK0
	CMP	#15
	BNE	TEST2
	STA	VAR
TEST2	LDA	CH
	CMP	#12
	BEQ	FREDDY

	LDA	TRIG0
	BEQ	FREDDY
	JMP	TSTRGT
FREDDY	JMP	BEEP

TEXIT	LDA	#255
	STA	CH
	JSR	BEEP
	JSR	BEEP
	JSR	PUTCUR
	JSR	DELAY
	LDA	VAR
	BNE	TLEAVE
	LDY	#40
	JSR	YLOOP
	LDA	#1
	STA	VAR
TLEAVE	JMP	TSTRGT

;************************************
;         PUT CURSOR AT XPOS,YPOS
;************************************
BOX2	DB	$FC,$84,$84,$84,$84,$84,$84,$84,$84,$84,$FC

PUTCUR	JSR	CLRPM3
	LDA	#1
	STA	SIZEP3
	LDA	#15
	STA	PCOLR3

	LDA	#PMBASE/256+3
	STA	ARG3HI

	LDA	#82
	LDY	XPOS
ADEM	CLC
	ADC	#12
	DEY
	BPL	ADEM
	STA	HPOSP3
	STA	TMPHPS

	LDA	#121
	LDY	YPOS
ADEM2	CLC
	ADC	#12
	DEY
	BPL	ADEM2

	STA	ARG3
	LDY	#10
B2LOOP	LDA	BOX2,Y
	STA	(ARG3),Y
	DEY
	BPL	B2LOOP
	RTS

;*************************************
;         DISPLAY NUMBERS
;         NUMBER PASSED IN A
;         PUT IN LOC ARG5
;*************************************
DNUMBR	LDX	#$FF
	STX	ARG4
	SEC
LOOP43	INX
	SBC	#100
	BCC	X62
	INC	ARG4
	JMP	LOOP43
X62	PHA
	TXA
	BEQ	X61
	ADC	#$10
	STA	(ARG5),Y
	INY
X61	PLA
	ADC	#100
	LDX	#$FF
	SEC
LOOP44	INX
	SBC	#10
	BCS	LOOP44
	PHA
	TXA
	BNE	X72
	BIT	ARG4
	BMI	X73
X72	ADC	#$10
	STA	(ARG5),Y
	INY
X73	PLA
	ADC	#$1A
	STA	(ARG5),Y
	INY
	LDA	#0
	STA	(ARG5),Y
	RTS

;************************************
;         SHOW ESP
;************************************
SHWESP	LDA	YPOS
	STA	TEMPI
	STA	LX
	STA	LZ
	LDA	XPOS
	STA	LY
	JSR	FLOVE2
	STA	TEMPST

	LDA	TEMPI
	STA	LX
	LDA	XPOS
	STA	LY
	LDA	#1
	STA	LZ
	JSR	FLOVE2
	LDA	TEMPST
	STA	(LOVESH),Y
	JMP	PTNSCN

;************************************
;         FIRE CUPID'S ARROWS
;************************************
FIRARW	JSR	DSHOOT
	LDA	YPOS
	STA	TEMPI
	STA	LX
	STA	LZ
	LDA	XPOS
	STA	LY
	JSR	FLOVE2
	JSR	INCVAL
	PHA
	STA	(LOVESH),Y

;      PUT INTO YOUR PERCEIVED
	LDA	XPOS
	STA	LY
	LDA	#1
	STA	LZ
	JSR	FLOVE2
	PLA
	STA	(LOVESH),Y
	PHA
	LDA	YPOS
	STA	LY
	STA	LZ
	LDA	XPOS
	STA	LX
	JSR	FLOVE2
	PLA
	STA	(LOVESH),Y

;************************************
;         PUT ON SCREEN
;************************************
PTNSCN	JSR	DIVD29
	PHA
	LDA	TEMPI
	STA	FX
	LDA	XPOS
	STA	FY
	JSR	FINDPS
	PLA
	STA	FELNGS,Y
	JSR	FILMAT
	JMP	CLRPM3

INCVAL	STA	TEMPST
	LDA	XPOS
	CMP	#1
	BEQ	YOUTOP
	LDA	TEMPST
	SEC
	SBC	#29
	BCS	ENDINC
	LDA	#0
	JMP	ENDINC
YOUTOP	LDA	TEMPST
	CLC
	ADC	#29
	BCC	ENDINC
	LDA	#255
ENDINC	STA	TEMPST
	RTS

;************************************
;      DISPLAY INTERRUPT
;************************************
DLI
;      ZEROITH DLI
;
FDLI
	PHA
	LDA	#70       ;RED
	STA	WSYNC
	EOR	COLRSH
	AND	DRKMSK
	STA	COLPF3
	LDA	#LOW FRDLI
	STA	DLVCTR
	PLA
	RTI

;
;      FIRST DLI
;
FRDLI
	PHA
	LDA	#LOW SEDLI
	STA	DLVCTR
	LDA	HAIRP4
	STA	HPOSP0
	LDA	HAIRP3
	STA	HPOSP1
	LDA	#90       ;???
	STA	WSYNC
	EOR	COLRSH
	AND	DRKMSK
	STA	COLPF0
	LDA	#AMODE4/256
	STA	CHBASE
	PLA
	RTI
;
;      SECOND DLI
;
SEDLI
	PHA
	LDA	#9        ;GRAY HAIR
	EOR	COLRSH
	AND	DRKMSK
	STA	COLPM0
	LDA	#140      ;BLUE SHIRT?
	STA	WSYNC
	EOR	COLRSH
	AND	DRKMSK
	STA	COLPF2
	LDA	#MAINST/256
	STA	CHBASE
	LDA	#LOW THADLI
	STA	DLVCTR
	PLA
	RTI
;
;       THIRD A DLI
;
THADLI
	PHA
	LDA	#15       ;WHITE
	STA	WSYNC
	STA	WSYNC
	STA	WSYNC
	STA	WSYNC
	STA	WSYNC
	STA	COLPF3
	LDA	#LOW THBDLI
	STA	DLVCTR
	PLA
	RTI
;
;       THIRD B DLI
;
THBDLI
	PHA
	LDA	#70       ;RED
	STA	WSYNC
	EOR	COLRSH
	AND	DRKMSK
	STA	COLPF3
	LDA	#LOW THCDLI
	STA	DLVCTR
	PLA
	RTI
;
;       THIRD C DLI
;
THCDLI
	PHA
	LDA	#AMODE4/256
	STA	WSYNC
	STA	CHBASE
	LDA	#LOW FODLI
	STA	DLVCTR
	PLA
	RTI
;
;      FORTH DLI
;
FODLI
	PHA
	LDA	HAIRP2
	STA	HPOSP1
	LDA	#MAINST/256
	STA	WSYNC
	STA	CHBASE
	LDA	#LOW FOBDLI
	STA	DLVCTR
	PLA
	RTI

;       FORTH AND A HALF DLI
FOBDLI
	PHA
	LDA	#AMODE4/256
	STA	WSYNC
	STA	CHBASE
	LDA	#LOW FIDLI
	STA	DLVCTR
	PLA
	RTI

;      FIFTH DLI

FIDLI
	PHA
	LDA	#224
	STA	CHBASE
	LDA	#100      ;PURPLE
	EOR	COLRSH
	AND	DRKMSK
	STA	WSYNC
	STA	COLBK
	LDA	#FDLI&255
	STA	DLVCTR
	LDA	HAIRP1
	STA	HPOSP0
	PLA
	RTI

;         ARRAYS FOR LOGIC

LOVE  	=	$8000	;ARRAYS SET UP 
LOVDIS	=	$8300	;HERE PURELY FOR
XARRAY	=	$8400	;DEBUG PURPOSES
YARRAY	=	$8500
STRTAB	=	$8600
RANKTB	DB	0,0,0,0	;RANKTABLES FOR
RNKTB1	DB	0,0,0,0	;FINAL RANK CALCS
ATIMAD	DB	1,0,0,1,0,1,0,1,0
TIMES 	DS	64
TTIMES	DS	8

FELNGS	DB	$09,$09,$09,$09,$09,$09,$09,$09
	DB	$09,$0A,$09,$09,$09,$09,$09,$09
	DB	$09,$09,$0A,$09,$09,$09,$09,$09
	DB	$09,$09,$09,$0A,$09,$09,$09,$09
	DB	$09,$09,$09,$09,$0A,$09,$09,$09
	DB	$09,$09,$09,$09,$09,$0A,$09,$09
	DB	$09,$09,$09,$09,$09,$09,$0A,$09
	DB	$09,$09,$09,$09,$09,$09,$09,$0A

DCL   	DS	9

;************************************
;*                                  *
;*   FINDPOS IS A SUBROUTINE THAT   *
;*  CALCULATES THE INDEX FOR A 2-D  *
;* MATRIX STORED IN 1-D. INPUTS ARE *
;*  FX,FY WHICH IS THE POSITION OF  *
;*  FX IN FY'S FRAME OF REFERENCE.  *
;*        Y=8*(FX-1)+FY-1           *
;*   Y IS THE FINAL VALUE OUTPUT    *
;*                                  *
;************************************

FINDPS	LDA	FX
	ASL	A
	ASL	A
	ASL	A
	CLC
	ADC	FY
	SEC
	SBC	#9
	TAY
	RTS

;************************************
;*                                  *
;*   FLOVE2 HAS INPUTS LY,LZ AND    *
;*   OUTPUT LY:                     *
;*          LY=8*(LY-1)+LZ          *
;*                                  *
;************************************

FLOVE2	LDA	LY
	ASL	A
	ASL	A
	ASL	A
	ADC	LZ
	SEC
	SBC	#8
	STA	LY

;************************************
;*                                  *
;* FNDLOV HAS INPUTS LY & LX. THE   *
;*   OUTPUT IS IN LOVESH AS THE     *
;* ADDRESS OF LOVE VALUE AND THE    *
;* LOVE VALUE IN THE ACCUMULATOR.   *
;* COMBINED WITH FLOVE2 THE ROUTINE *
;* WILL INDEX A 3-D MATRIX          *
;*     LOVESH=LOVE+64*(LX-1)+LY     *
;*                         :LX<8    *
;*                                  *
;************************************
FNDLOV	LDA	#HIGH LOVE
	STA	LOVESH+1
	DEC	LX
	LDA	LX
	CLC
	ROR	A
	ROR	A
	ROR	A
	BCC	ZEPPO
	INC	LOVESH+1

ZEPPO	CLC
	ADC	LY
	BCC	CHICO
	INC	LOVESH+1

CHICO	CLC
	ADC	#LOW LOVE
	BCC	GROUCH
	INC	LOVESH+1

GROUCH	STA	LOVESH
	LDY	#0
	LDA	(LOVESH),Y
	INC	LX
	RTS

;************************************
;*                                  *
;*  MULTIPLY MULTIPLIES 2 8-BIT NOS *
;* AND PUTS THE PRODUCT IN A 16 BIT *
;* VARIABLE PRODHI AND LO           *
;*   WITHIN COGITATE THE NUMBERS    *
;* MUST BE MADE ABSOLUTE SO ABSVAL  *
;* IS PREFIXING THE SUBROUTINE AND  *
;* HAS NO RTS                       *
;*                                  *
;************************************

ABSVAL	BCS	ENDABS
	EOR	#$FF
	CLC
	ADC	#1
ENDABS	STA	MULTPR
	STA	DIVISR

MULTPY	LDA	#0
	STA	PRODLO
	STA	PRODHI
	STA	TEMPMU

LOOP31	LSR	MULTPR
	BCC	Y11
	CLC
	LDA	PRODLO
	ADC	DIVISR
	STA	PRODLO
	LDA	PRODHI
	ADC	TEMPMU
	STA	PRODHI

Y11	LDA	MULTPR
	BEQ	Y12
	ASL	DIVISR
	ROL	TEMPMU
	BCC	LOOP31
Y12	RTS

;************************************
;*                                  *
;* DIVIDE TAKES AN 8-BIT NUMBER AND *
;* RETURNS AN 8-BIT RESULT IN PROD  *
;* INPUTS ARE DIVISR I THINK        *
;*                                  *
;************************************

DIVIDE	LDY	#8
LOOP34	ASL	PRODLO
	ROL	PRODHI
	BCC	Y13
	LDA	PRODHI
	SBC	DIVISR
	JMP	Y14
Y13	LDA	PRODHI
	SEC
	SBC	DIVISR
	BCC	Y15
Y14	STA	PRODHI
	INC	PRODLO
Y15	DEY
	BNE	LOOP34
	RTS

;************************************
;*                                  *
;* SQUARE ROOT TAKES A 16-BIT NUMBER*
;* AND CALCULATES THE ROOT WHICH IS *
;* STORED IN SQRTA                  *
;*                                  *
;************************************

SQROOT	LDX	#$F
LOOP06	ASL	ACCLO
	ROL	ACCHI
	BCS	X09
	DEX
	BPL	LOOP06
	LDA	#0
	STA	SQRTA
	RTS
X09	ROR	ACCHI
	ROR	ACCLO
	TXA
	LSR	A
	CLC
	ADC	#1
	STA	NBITS
X20	INX
	CPX	#$10
	BEQ	X19
	LSR	ACCHI
	ROR	ACCLO
	JMP	X20
X19	LDX	NBITS
	LDA	BITTAB,X
	STA	SQRTA
LOOP07	TAY
	LDA	SQRTHI,Y
	CMP	ACCHI
	BCC	X10
	BNE	X11
	LDA	SQRTLO,Y
	CMP	ACCLO
	BCC	X10
	BEQ	X12
X11	LDA	BITTAB,X
	EOR	SQRTA
	STA	SQRTA
X10	LDA	BITTAB-1,X
	ORA	SQRTA
	STA	SQRTA
	DEX
	BEQ	X12
	JMP	LOOP07
X12	RTS

BITTAB	DB	0,1,2,4,8,16,32,64,128


;************************************
;*                                  *
;* DIVIDE BY 29 ONLY TO PARTITION   *
;* LOVE VALUES INTO THE NINE DEGREES*
;*                                  *
;************************************

DIVTAB	DB	0,28,57,85,113,141,170,198,227
DIVD29	LDY	#8
DIVLUP	CMP	DIVTAB,Y
	BCS	DOUT
	DEY
	JMP	DIVLUP
DOUT	TYA
	RTS

;************************************
;*                                  *
;* THE TWO BYTE DIVIDE TAKES A 16   *
;* BIT NUMBER AND RETURNS AN 8 BIT  *
;* NUMBER. IN THIS CASE PRODLO/HI   *
;* CONTAINS $49D4 AND IS DIVIDED BY *
;* THE NUMBER STORED IN DIVRLO/HI   *
;* THE 8-BIT RESULT IS IN PRODLO    *
;*                                  *
;************************************

TWODIV	LDY	#16
	LDA	#0
	STA	MULTLO
	STA	MULTHI
	LDA	#$D4
	STA	PRODLO
	LDA	#$49
	STA	PRODHI
KLOP11	ASL	PRODLO
	ROL	PRODHI
	ROL	MULTLO
	ROL	MULTHI
	LDA	MULTLO
	SEC
	SBC	DIVRLO
	TAX
	LDA	MULTHI
	SBC	DIVRHI
	BCC	KSTEP1
	STA	MULTHI
	STX	MULTLO
	INC	PRODLO
KSTEP1	DEY
	BNE	KLOP11
	RTS

;************************************
;*                                  *
;* CHECK LIMITS MAKES SURE THAT THE *
;* ACCUMULATOR IS BETWEEN 38 AND 218*
;*                                  *
;************************************

CHKLIM	CMP	#38
	BCS	NOTLOW
	LDA	#38
NOTLOW	CMP	#219
	BCC	NOTHIH
	LDA	#218
NOTHIH	RTS

;************************************
;*                                  *
;* THIS SUBROUTINE GETS RANDOM VALS *
;* FOR THE LOVE ARRAY AND ADJUSTS   *
;* X,Y AND LOVE VALUES UNTIL THE    *
;* LIMITS ARE SATISFIED. X AND THE  *
;* SQUARE ARE RANDOMISED AND Y IS   *
;* OBTAINED BY DEFAULT. IF IT DOES  *
;* NOT FIT WITHIN THE LIMITS THEN   *
;* REPEAT UNTIL IT DOES             *
;*                                  *
;************************************

KRNDOM	STA	(LOVESH),Y
	LDA	LOVDIS,X
	STA	GEODIS
	STA	MULTPR
	STA	DIVISR
	JSR	MULTPY
	LDA	PRODLO
	STA	ACCLO
	LDA	PRODHI
	STA	ACCHI
KFLOP2	LDA	RANDOM
	JSR	CHKLIM
	STA	TEMPX
	SEC
	SBC	#128
	JSR	ABSVA2
	CMP	GEODIS
	BEQ	XEQGEO
	BCS	KFLOP2
	STA	MULTPR
	STA	DIVISR
	JSR	MULTPY
	LDA	ACCLO
	SEC	
	SBC	PRODLO
	STA	ACCLO
	LDA	ACCHI
	SBC	PRODHI
	STA	ACCHI
	JSR	SQROOT
	LDA	SQRTA
	CMP	#90
	BEQ	GETXAY
	BCS	KFLOP2
GETXAY	LDA	RANDOM
	CMP	#128
	BCS	PLUS
MINUS	LDA	#128
	SEC
	SBC	SQRTA
	JSR	ABSVA2
	JMP	STAXAY
PLUS	LDA	#128
	CLC
	ADC	SQRTA
	JSR	ABSVA2
	JMP	STAXAY
XEQGEO	LDA	#$80
STAXAY	STA	TEMPY
	JSR	FINDPS
	LDA	RANDOM
	CMP	#128
	BCS	XFIRST
YFIRST	LDA	TEMPX
	STA	YARRAY,Y
	LDA	TEMPY
	STA	XARRAY,Y
	JMP	KENDRD
XFIRST	LDA	TEMPX
	STA	XARRAY,Y
	LDA	TEMPY
	STA	YARRAY,Y
KENDRD	RTS
ABSVA2	BCS	ENDAB2
	EOR	#$FF
	CLC
	ADC	#1
ENDAB2	RTS

DIRXTB	DB	0,0,1,$FF,$FF
DIRYTB	DB	0,1,$FF,$FF,1



;************************************
;*                                  *
;* SET VBV ROUTINE :SEE ARIC        *
;*                                  *
;************************************

SETVBV	PHA
SETLUP	LDA	VCOUNT
	CMP	#40
	BCC	SETLUP
	CMP	#190
	BCS	SETLUP
	PLA
	ASL	A
	STA	SETTMP
	TXA
	LDX	SETTMP
	STA	CDTMV1-1,X
	TYA
	STA	CDTMV1-2,X
	RTS

;***********************************
;*         COGITATE                *
;* THE ROUTINE TO RE-EVALUATE THE  *
;* LOVE VALUES OF THE PLAYERS.LOVE *
;* AND XPOS,YPOS ARE THE INPUT     *
;* PARAMETERS AND THE OUTPUT PARA- *
;* METERS. THE METHOD IS MINIMISA- *
;* TION OF STRESS BETWEEN PLAYERS  *
;* BY LEAST SQUARES. THE ITERATNS  *
;* CEASE WHEN ALL PLAYERS IN EACH  *
;* REFERENCE FRAME HAVE MINIMISED  *
;* THEIR POSITION.                 *
;***********************************
COG	LDY	#15	;PLEASE WAIT 
	JSR	DMESS	;MESSAGE
	LDA	#0
	STA	TEMCTR
	LDA	NSQARE	;N FRAMES OF REF
	SEC		;WITH N-1 OBJECTS
	SBC	N	;PER FRAME.STORE
	STA	STRCTR	;IN STRESS COUNTR
KIMAIN	LDX	#$40	;STRESS TAB TO 
	LDA	#1	;SEE WHICH PLYR 
TIM3LP	STA	STRTAB,X	;IN A FRAME HAS
	DEX		;CEASED TO MOVE
	BPL	TIM3LP	;REACHED MIN STRS
	LDY	N	;I:FRAME OF REF
ISLOOP	STY	I
	STY	FY	;FX,FY=J,I
	STY	LZ	;LX,LY,LZ=J,K,I
	LDY	N
JSLOOP	STY	J	;J FEELS ABOUT K
	CPY	I	;FROM I'S VIEW
	BNE	ANDSON	;IF I=J NEXT J
	JMP	ENDOFJ
ANDSON	STY	FX	;FINDPS(J,I)=J'S
	STY	LX	;POSN IN I'S FRAM
	JSR	FINDPS	;ALSO=Y INDEX FOR
	LDA	STRTAB,Y	;STRSTAB. IF=1 
	BNE	KNTINU	;GO ON. IF=0 THEN
	JMP	ENDOFJ	;NEXT J
KNTINU	LDA	XARRAY,Y	;GET XPOS OF J ON
	STA	TEMPX	;I
	LDA	YARRAY,Y	;GET YPOS OF J ON
	STA	TEMPY	;I
	LDA	#$FF	;MAXIMISE STRESS
	STA	STRSLO
	STA	STRSHI
	LDX	#0	;ZERO DIRECTION
	STX	DIRCTN	;OF INCREMENT
DIRLOP	LDA	TEMPX	;ADD X INC IN 
	CLC		;DIRCTN TO TEMPX
	ADC	DIRXTB,X
	JSR	CHKLIM	;CHECK WITHIN 
	STA	TEMPX	;FRAME LIMITS
	LDA	TEMPY	;ADD Y INC IN 
	CLC		;DIRCTN OT TEMPX
	ADC	DIRYTB,X
	JSR	CHKLIM	;CHECK WITHIN 
	STA	TEMPY	;FRAME LIMITS
	LDA	#0	;MINIMISE ACCUM-
	STA	SUMLOW	;ULATIVE SUM
	STA	SUMHIH
BKLOOP	LDY	N	;GET K; IF K=J
KSLOOP	STY	K	;NEXT K
	CPY	J
	BEQ	ENDOFK
	STY	FX	;FP(K,I)
	STY	LY	;FL(J,K,I)
	JSR	FLOVE2	;IF LOV=0 NEXT K
	BEQ	ENDOFK
	JSR	FINDPS	;FIND K'S POS IN
	LDA	TEMPX	;I
	SEC		;A=X(J)-X(K)
	SBC	XARRAY,Y
	JSR	ABSVAL	;A=|X(J)-X(K)|
	LDA	PRODLO	;PROD=A*A
	STA	ACCLO	;ACC=PROD
	LDA	PRODHI
	STA	ACCHI
	LDA	TEMPY	;A=Y(J)-Y(K)
	SEC
	SBC	YARRAY,Y
	JSR	ABSVAL	;A=|Y(J)-Y(K)|
	LDA	PRODLO	;PROD=A*A
	CLC		;ACC=ACC+PROD
	ADC	ACCLO	;I.E.
	STA	ACCLO	;ACC=DELX*DELX+
	LDA	PRODHI	;DELY*DELY
	ADC	ACCHI	;
	STA	ACCHI	;GET GEOMETRIC
	JSR	SQROOT	;DIS BETWEEN J&K
	LDY	#0	;GET LOVE(J,K,I)
	LDA	(LOVESH),Y
	TAX		;CONVERT TO GEO-
	LDA	LOVDIS,X	;METRIC DISTANCE
	SEC
	SBC	SQRTA	;A=REAL-DESIRED
	JSR	ABSVAL	;PROD=A*A
	LDA	SUMLOW	;SUM=SUM+PROD
	CLC		;I.E.
	ADC	PRODLO	;SUM=SUM OF SQRS
	STA	SUMLOW	;OF DIFFERENCE
	LDA	SUMHIH	;BETWEEN REAL &
	ADC	PRODHI	;DESIRED DISTANCE
	STA	SUMHIH
ENDOFK	LDY	K	;NEXT K LOOP
	DEY
	BEQ	TSTDIR	;IF K=0 THEN TEST
	JMP	KSLOOP	;NEXT DIRECTION
TSTDIR	LDA	DIRCTN	;IF<>0 THEN START
	BNE	PERMST	;PERMSTORE RTN
	LDA	TEMCTR	;DIVIDE TEMCTR BY
	LSR	A	;16:SUBTRACT FROM
	LSR	A	;THE STRESS AT 
	LSR	A	;REST AND STORE
	LSR	A	;IN A ZERO STRESS
	STA	ACCLO	;VARIABLE.
	LDA	SUMLOW	;IF STRESS IN ANY
	SEC		;OF THE OTHER 
	SBC	ACCLO	;DIRECTIONS IS<=
	STA	SUMLOW	;ZERO STRESS THEN
	STA	ZESTLO	;CEASE STRESS 
	LDA	SUMHIH	;COMPUTATIONS
	SBC	#0
	STA	SUMHIH
	STA	ZESTHI
PERMST	LDA	STRSHI	;STRESSH><=SUMHI?
	CMP	SUMHIH	;
	BCC	INCDIR	;STH>SMH:CHGE DIR
	BNE	STEQSM	;STH<SMH:ST=SM
	LDA	STRSLO	;STH=SMH:TEST LO
	CMP	SUMLOW	;STL>SML:CHGE DIR
	BCC	INCDIR	;STL<SML:ST=SM
STEQSM	LDA	SUMLOW	;STL=SML:ST=SM
	STA	STRSLO
	LDA	SUMHIH
	STA	STRSHI
	LDA	J	;GET FP(J,I) INDX
	STA	FX
	JSR	FINDPS
	LDA	TEMPX	;STORE NEW X,
	STA	XARRAY,Y	;(X+DIR) IN XPOS
	LDA	TEMPY	;STORE NEW Y,
	STA	YARRAY,Y	;(Y+DIR) IN YPOS
INCDIR	INC	DIRCTN	;DIRECTION CHANGE
	LDX	DIRCTN
	CPX	#5	;IF DIRS EXHAUSTD
	BEQ	BSTZRO	;TEST BEST STRESS
	JMP	DIRLOP	;WITH ZERO STRESS
BSTZRO	LDA	STRSHI	;STH<ZEH: NEXT J
	CMP	ZESTHI	;STH>ZEH: TEST LO
	BCC	ENDOFJ	;STH=ZEH: TEST LO
	LDA	STRSLO	;STL<ZEL: NEXT J
	CMP	ZESTLO	;STL>ZEL: STP CAL
	BCC	ENDOFJ	;STL=ZEL: STP CAL
STOPCL	LDA	#0	;STOP CALC:STOR 0
	STA	STRTAB,Y	;IN STRS TABLE
	DEC	STRCTR	;DEC COUNTER
	BMI	KLSTLY	;IF CNTER<0 EXIT
ENDOFJ	LDY	J
	DEY
	BEQ	ENDOFI
	JMP	JSLOOP
ENDOFI	LDY	I
	DEY
	BEQ	KLSTIT
	JMP	ISLOOP
KLSTIT	INC	TEMCTR	;INC ITERATN CNTR
	JMP	KIMAIN	;NEXT ITERATION

KLSTLY	LDY	N	;PUT NEW LOVE VAL
KFINLY	STY	I	;INTO THE ARRAY
	STY	LX	;FL(I, ,I)
	STY	LZ	;
	STY	FY	;FP( ,I)
	LDY	N
KMVLOP	STY	J	;I=J:THEN NEXT J
	CPY	I
	BEQ	KNIJ
	STY	FX	;FP(J,I)
	STY	LY	;FL(I,J,I)
	JSR	FINDPS
	LDA	XARRAY,Y	;GET|XPOS-128|**2
	SEC	
	SBC	#$80
	JSR	ABSVAL
	LDA	PRODLO	;ACC=PROD
	STA	ACCLO
	LDA	PRODHI
	STA	ACCHI
	LDA	YARRAY,Y	;GET|YPOS-128|**2
	SEC
	SBC	#$80
	JSR	ABSVAL
	LDA	PRODLO	;ACC=ACC+PROD
	CLC		;I.E.
	ADC	ACCLO	;ACC=X*X+Y*Y
	STA	ACCLO
	LDA	PRODHI
	ADC	ACCHI
	STA	ACCHI
	JSR	SQROOT	;SQRTA=GEODIS(J)
	JSR	FLOVE2	;CONVERT TO THE
	LDY	#0	;LOVE VALUE AND
	LDX	SQRTA	;STORE IN LOVE
	LDA	LOVDIS,X	;ARRAY
	STA	(LOVESH),Y
KNIJ	LDY	J
	DEY
	BNE	KMVLOP
KNII	LDY	I
	DEY
	BNE	KFINLY
	LDA	NUMARR	;INCREASE THE 
	CLC		;ARROWS BY BASE
	ADC	ARRNUM	;ARROW NUMBER
	STA	NUMARR
	RTS

