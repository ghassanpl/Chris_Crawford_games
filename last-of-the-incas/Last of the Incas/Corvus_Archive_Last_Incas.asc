



;LOT07N.ASM  MARCH 5, 1984
;news queue handling routines

;scans news queue for an active entry

SCNWSQ	LDX	NWQPTR
	BEQ	X80
	DEX
LOOP10	STX	SAVEX
	DCMP	2NWQT,X.2GMCK
	BCS	X85
	LDY	NWQEVT,X	;news has arrived
	LDA	NWQTWN,X
	JSR	TELTWN
	LDX	SAVEX	;collapse stack
LOOP11	LDA	NWQEVT+1,X
	STA	NWQEVT,X
	LDA	NWQTWN+1,X
	STA	NWQTWN,X
	LDA	L2NWQT+1,X
	STA	L2NWQT,X
	LDA	H2NWQT+1,X
	STA	H2NWQT,X
	INX
	CPX	NWQPTR
	BNE	LOOP11

	DEC	NWQPTR
X85	LDX	SAVEX
	BEQ	X80
	DEX
	JMP	LOOP10
X80	RTS

;notifies town given in A of event
;given in Y-register.
;Output sets appropriate bit in KNWSKNW array
;and adds notification reminders to news queue stack.

TELTWN	STA	SAVTWN
	JSR	TWNBIT	;first check if town already knows
	STX	TEMP2
LOOP12	ROR	A
	DEX
	BPL	LOOP12
	BCC	X86
	RTS	;get out if town already knows
X86	LDX	TEMP2	;town doesn't know, make it know
	SEC
LOOP13	ROL	A
	DEX
	BPL	LOOP13
	STA	(KNWPTR),Y

	LDA	#3	;notify 4 contiguous towns
LOOP85	STA	TEMP2	;saves dir
	JSR	FIVASL
	ADC	SAVTWN
	TAX
	STA	TEMP4
	LDA	UPCONT,X
	BMI	X83	;town in this dir?
	AND	#$1F	;yes
	STA	TEMP3
	TAX
	LDA	ATNOWN,X
	AND	#4
	BEQ	X88	;Spanish?
	LDX	SAVTWN	;yes
	LDA	ATNOWN,X
	AND	#4
	BEQ	X83	;Spanish?
X88	LDA	TEMP3	;yes
	JSR	TWNBIT
LOOP84	ROR	A
	DEX
	BPL	LOOP84
	BCS	X83	;town already knows?

	LDX	TEMP4	;no, put it onto news queue
	DCALC	TTBTWN,X
	DASL	2PROD
	DASL	2PROD
	DASL	2PROD
	DASL	2PROD	;note that messengers travel twice as fast as armies
	LDX	NWQPTR
	DCALC	+.2GMCK.=.2NWQT,X
	TYA
	STA	NWQEVT,X
	LDA	TEMP3
	STA	NWQTWN,X
	INC	NWQPTR
	BNE	X83	;stack overflow?
	STX	ERRFLG	;yes
X83	LDA	TEMP2
	SEC
	SBC	#1
	BPL	LOOP85

	LDA	TRUPOS
	CMP	SAVTWN
	BNE	X89	;arrived at Manco?
	LDA	NWSAPU,Y	;yes
	LSR	A
	LSR	A
	LSR	A
	LSR	A
	LSR	A
	TAX
	LDA	NWSAPU,Y
	AND	#$1F
	STA	APUTWN,X
	CPX	APU
	BNE	X89
	LDA	#$FF
	STA	NAPFLG

X89	RTS

;sets up KNWPTR to point to town in A and event in Y-reg.
;Returns with X pointing to bit and A containing byte.

TWNBIT	PHA
	AND	#7
	TAX
	PLA
	LSR	A
	LSR	A
	LSR	A
	CLC
	ADC	#HIGH NWSKNW
	STA	KNWPTR+1
	LDA	#LOW NWSKNW
	STA	KNWPTR
	LDA	(KNWPTR),Y
	RTS

;creates a news event
;input: 
;	A: town entered
;	X: APU perpetrator
;	Y: don't care

MAKNWS	LDY	NWSCNT
	STA	TEMP1
	LDA	H2GMCK
	STA	NWSDAY,Y
	TXA
	JSR	FIVASL
	ORA	TEMP1
	STA	NWSAPU,Y

	LDA	TEMP1
	JSR	TELTWN
	INC	NWSCNT
	RTS





;LOT07G.ASM  MARCH 5, 1984
;MESSENGER ROUTINES

;master messenger execution routine
;no inputs required (Wow! ESP comes to programming!)

MSGREX	LDX	#$1F
LOOP20	STX	MSGRNM
	LDA	L2MGTM,X
	BNE	X67
	LDA	H2MGTM,X
	BEQ	X60
X67
	DCMP	2MGTM,X.2GMCK
	BCC	X59

X60	LDX	MSGRNM
	DEX	;not arrived yet
	BPL	LOOP20
	RTS	;only exit point

X59	LDA	MSGRTN,X	;messenger has arrived at a town
	LDY	MSGRAP,X
	CMP	TRUPOS,Y
	BNE	X68	;is his APU there?
;yes---transfer orders

	DCALC	#0
	TXA
	JSR	FIVASL
	ROL	H2PROD
	ADC	#LOW MSGMSG
	STA	L2PROD
	LDA	H2PROD
	ADC	#HIGH MSGMSG
	STA	H2PROD

	TYA
	JSR	FIVASL
	TAX

	LDY	MSGRNM
	LDA	MSGRCT,Y
	STA	TEMP1	;stop count value
	LDY	#0

;attempt to find a match between old and new orders

	NOP	;this is a stub
LOOP23	LDA	(L2PROD),Y
	STA	APUORD,X
	INX
	INY
	CPY	TEMP1
	BNE	LOOP23
	LDY	MSGRNM
	LDA	MSGRCT,Y
	LDX	MSGRAP,Y
	STA	APORCT,X
	STX	MLNAPU
	TXA
	JSR	FIVASL
	TAY
	JSR	CALOTM	;set it in motion

;retire messenger from duty

	LDX	MSGRNM
	LDA	#0
	STA	L2MGTM,X
	STA	H2MGTM,X

;notify slowpoke we got it

	LDA	#40
	STA	AUDF2
	LDA	#$A4
	STA	AUDC2
	LDA	#30
	JSR	DELAY
	LDA	#0
	STA	AUDC2

	JMP	X60


X68	JSR	SRNWDB
	PHP
	LDX	MSGRNM
	PLP
	BEQ	X61	;any news?
	CMP	MSGRDY,X	;yes
	BCC	X61	;better news?
	STA	MSGRDY,X	;yes
	LDA	NWSAPU,Y
	AND	#$1F
	STA	MSGRGO,X	;set new immediate destination

X61	LDY	MSGRGO,X
	LDA	MSGRTN,X
	CPX	#$10
	TAX
	LDA	#$FF
	BCS	X65
	LDA	#0
X65	JSR	QUOVAD
	BMI	X66	;we're there; just wait
	JSR	FIVASL
	STA	TEMP1
	TXA
	LDX	MSGRNM
	LDY	MSGRTN,X
	STA	MSGRTN,X
	TYA
	ORA	TEMP1
	TAY
	DCALC	TTBTWN,Y
	DASL	2PROD
	DASL	2PROD
	DASL	2PROD
	DASL	2PROD
	DCALC	+.2GMCK.=.2MGTM,X

X66	JMP	X60

;subroutine SRNWDB
;"search news data base"
;Input
;	A: Town whose database is to be searched
;	X: Don't care
;	Y: APU about whom news is desired

;Output
;	A: date of most recent news
;	X: affected
;	Y: Most recent news message on APU, TWN
;	Z: Z=1 => Y is invalid

SRNWDB	STY	SRCAPU
	JSR	TWNBIT
	LDY	#0
	STY	TEMP3
LOOP67	STY	SRCEVT
	LDA	NWSAPU,Y
	LSR	A
	LSR	A
	LSR	A
	LSR	A
	LSR	A
	CMP	SRCAPU
	BNE	Y04
	LDA	(KNWPTR),Y
	AND	SRCMSK,X
	BEQ	Y04
	LDA	NWSDAY,Y
	CMP	TEMP1
	BCC	Y04
	INC	TEMP3
	STA	TEMP1
	STY	TEMP2
Y04	INY
	CPY	NWSCNT
	BNE	LOOP67
	LDY	TEMP2
	LDA	TEMP1
	LDX	TEMP3
	RTS

SRCMSK	DB	1,2,4,8,16,32,64,128

DELAY	CLC
	ADC	RTCLKL
LOOP21	CMP	RTCLKL
	BNE	LOOP21
	RTS

;Subroutine QUOVAD
;"where are you going?"
;Input
;	A: 0 if Inca is mover, $FF is Spanish
;	X: town of departure
;	Y: destination

;Output
;	A: direction to go ($FF => already there!)
;	X: town to go to
;	Y: ?

QUOVAD	STA	TEMP2
	STX	TEMP3
	CPY	TEMP3
	BNE	X04
	LDA	#$FF
	RTS
X04	TYA
	ASL	A
	ASL	A
	ASL	A
	STA	TEMP1
	TXA
	AND	#3
	TAX
	LDA	TEMP3
	LSR	A
	LSR	A
	ORA	TEMP1
	STY	TEMP1
	TAY
	BIT	TEMP2
	BMI	X26
	LDA	QUIDAT,Y
	JMP	X44
X26	LDA	QUSDAT,Y
X44	DEX
	BMI	X45
	LSR	A
	LSR	A
	JMP	X44
X45	AND	#3
	PHA
	JSR	FIVASL
	ORA	TEMP3
	TAY
	LDA	UPCONT,Y
	AND	#$1F
	TAX
	LDY	TEMP1
	PLA
	RTS

QUIDAT	DB	$A8,$A5,$80,$0F,$20,$08,$00,$00
	DB	$A3,$AF,$80,$3F,$20,$08,$03,$00
	DB	$83,$0F,$80,$3F,$20,$08,$03,$00
	DB	$33,$3F,$03,$3F,$30,$0F,$3F,$00
	DB	$AA,$A8,$AA,$23,$A0,$08,$00,$00
	DB	$AA,$A3,$88,$2F,$20,$08,$00,$00
	DB	$5F,$8F,$8F,$3F,$30,$0F,$03,$00
	DB	$7F,$3F,$0F,$3F,$30,$0F,$3F,$00
	DB	$AA,$A5,$A8,$2F,$A0,$08,$00,$00
	DB	$A7,$95,$A3,$3F,$A0,$08,$00,$00
	DB	$5F,$9D,$8F,$3F,$30,$0F,$0F,$00
	DB	$1F,$5D,$3F,$3F,$3C,$1F,$3F,$00
	DB	$A6,$95,$AA,$28,$A0,$98,$2A,$00
	DB	$A6,$95,$AA,$23,$20,$08,$00,$00
	DB	$A5,$95,$AB,$0F,$A0,$0F,$03,$00
	DB	$A7,$95,$A7,$3F,$A0,$0F,$0F,$00
	DB	$A5,$95,$AB,$1F,$A0,$0F,$33,$00
	DB	$67,$55,$67,$7F,$A0,$1F,$0F,$00
	DB	$57,$55,$77,$7F,$8C,$1F,$0F,$00
	DB	$5F,$55,$77,$7F,$3C,$1F,$3F,$00
	DB	$66,$55,$66,$59,$65,$98,$2A,$00
	DB	$66,$55,$66,$59,$65,$93,$2A,$00
	DB	$65,$55,$6B,$1D,$65,$8F,$23,$00
	DB	$5F,$55,$76,$75,$75,$1F,$3F,$00
	DB	$65,$55,$66,$55,$65,$9F,$28,$00
	DB	$65,$55,$67,$55,$65,$9F,$23,$00
	DB	$67,$55,$67,$75,$65,$9F,$0F,$00

QUSDAT	DB	$A8,$A5,$83,$0F,$3C,$18,$3A,$00
	DB	$A3,$A5,$81,$3F,$3C,$18,$3F,$00
	DB	$83,$05,$83,$3F,$3C,$18,$3F,$00
	DB	$30,$35,$03,$3F,$3C,$1F,$3F,$00
	DB	$A4,$A4,$8A,$23,$24,$A8,$2A,$00
	DB	$A4,$A0,$88,$2F,$3C,$08,$3A,$00
	DB	$5C,$85,$83,$3F,$3C,$1F,$3F,$00
	DB	$7C,$35,$03,$3F,$3C,$0F,$3F,$00
	DB	$A4,$A5,$88,$2F,$3C,$18,$3A,$00
	DB	$A4,$A5,$83,$3F,$3C,$18,$3E,$00
	DB	$5C,$95,$83,$3F,$3C,$1F,$3F,$00
	DB	$1C,$55,$33,$3F,$3C,$1F,$3F,$00
	DB	$A4,$A5,$8A,$28,$64,$98,$2A,$00
	DB	$A4,$A5,$AA,$23,$3C,$18,$2A,$00
	DB	$A4,$A5,$8B,$0F,$3C,$18,$3A,$00
	DB	$A4,$A5,$87,$3F,$3C,$A8,$3E,$00
	DB	$A4,$A5,$8B,$1F,$3C,$A8,$3A,$00
	DB	$5C,$55,$72,$39,$A0,$1F,$0F,$00
	DB	$5C,$55,$73,$3D,$8C,$1F,$0F,$00
	DB	$5C,$55,$73,$3D,$3C,$1F,$3F,$00
	DB	$64,$55,$7A,$29,$64,$98,$2A,$00
	DB	$64,$55,$7A,$29,$64,$93,$2A,$00
	DB	$54,$55,$72,$29,$64,$8F,$23,$00
	DB	$5C,$55,$72,$39,$74,$1F,$3F,$00
	DB	$5C,$55,$7A,$29,$64,$9F,$28,$00
	DB	$5C,$55,$72,$29,$64,$9F,$23,$00
	DB	$5C,$55,$72,$39,$64,$9F,$0F,$00






;LOT07I.ASM  MARCH 6, 1984
;VBI service routine

VBISRV

;game clock service

	LDA	RTCLKL
	AND	#$F
	BNE	X99
	DINC	2GMCK

;road traversal

X99	LDA	RTCLKL
	AND	#1
	BNE	X77
	JMP	X40
X77	LDA	TICK
	BPL	X78
X46	JMP	X79
X78	INC	TRVLTM
	DEC	TCKDEL
	BPL	X46
	LDA	DIR
	ASL	A
	ASL	A
	ORA	TICK
	ASL	A
	ASL	A
	TAY
	LDA	#$FF
	STA	TEMPI
	LDX	#0
LOOP70	LDA	M0PF,X
	AND	MMASK,Y
	BEQ	X70
	CPX	DIR
	BEQ	X71
	CPX	VRBTNX
	BEQ	X70
	STX	TEMPI
X70	INY
	INX
	CPX	#4
	BNE	LOOP70
	LDX	TEMPI
	BPL	X71
	BMI	X73	;no collision obtained
;collision in X-reg
X71	LDA	TICK
	CMP	#2	;patch for odd shape
	BNE	X69
	CPX	DIR
	BNE	X69
	LDA	PATCHX,X
	TAX
X69	LDA	HIKEX
	CLC
	ADC	XOFFTB,X
	STA	HIKEX
	LDA	HIKEY
	CLC
	ADC	YOFFTB,X
	STA	HIKEY
	TXA
	EOR	#1
	STA	VRBTNX
	LDA	#$FF
	STA	TICK

;check for arrival at destination

	LDY	DESTWN
	LDA	YTOWN,Y
	CLC
	ADC	#5
	SEC
	SBC	HIKEY
	JSR	ABSVAL
	CMP	#4
	BCS	X73
	LDA	XTOWN,Y
	CLC
	ADC	#3
	SEC
	SBC	HIKEX
	JSR	ABSVAL
	CMP	#4
	BCS	X73

;arrived at destination

	JSR	CLRMIS

;display travel time

	LDA	TRVLTM
	LDY	#$40
	JSR	DNUMBR
	JMP	X79

X73	LDY	TICK
	INY
	CPY	#4
	BNE	X74
	LDA	#0	;flag error
	STA	COLOR4
	LDA	#$FF
	STA	TICK
	JMP	X79
TCKDLT	DB	0,1,2,4

X74	STY	TICK
	LDA	TCKDLT,Y
	STA	TCKDEL
	JSR	CLRMIS
	JSR	DRWMIS

X79	STA	HITCLR

;handle stepcount manipulations

X40	LDA	TICK
	BMI	X76	;don't step if hiker is busy
X75	JMP	X17
X76	LDA	RTCLKL
	AND	#$F
	CMP	#4
	BNE	X75
	LDX	APU
	TXA
	AND	#4
	BNE	X75
	LDA	ORDCNT,X
	BEQ	X75
	LDY	STPTWN
	LDX	TWNOWN,Y
	CPY	LAMTWN
	BNE	X62
	LDX	#16
X62	JSR	MVICON

	INC	STPCNT

	LDY	APU
	LDA	STPCNT
	CMP	ORDCNT,Y
	BCC	X23	;last order?
	LDA	CURTWN
	STA	STPTWN
	LDA	#0
	STA	STPCNT
X23	LDA	APU
	JSR	FIVASL
	ORA	STPCNT
	TAX
	LDA	STPCNT
	BEQ	X64
	DEX
	LDA	ORDERS,X
	AND	#$1F
	STA	STPTWN
	INX
X64	LDA	ORDERS,X
	AND	#$1F
	STA	DESTWN
	LDA	ORDERS,X
	LSR	A
	LSR	A
	LSR	A
	LSR	A
	LSR	A
	STA	DIR
	CLC
	ADC	#8
	TAX
	LDY	STPTWN
	JSR	MVICON

;initialize hike routine

	LDA	DIR
	CMP	#4
	BCC	X17
	AND	#3
	STA	DIR
	EOR	#1
	STA	VRBTNX
	LDY	STPTWN
	LDA	YTOWN,Y
	STA	HIKEY
	LDA	XTOWN,Y
	STA	HIKEX
	LDA	#0
	STA	TICK
	STA	TRVLTM
	LDA	#1
	STA	TCKDEL
	JSR	DRWMIS

;blink town under cursor

X17	LDY	CURTWN
	LDA	RTCLKL
	AND	#$1F
	BNE	X18
	LDA	TWNMSK,Y
	EOR	#$FF
	STA	TWNMSK,Y
X18	LDA	APU
	AND	#4
	BEQ	X11
	JMP	X20

X11	LDA	RTCLKL
	AND	#$3F
	CMP	#$30
	BNE	X14
	LDY	LAMTWN
	LDX	TWNOWN,Y
	JSR	MVICON
	JMP	X15

X14	CMP	#$10
	BNE	X15
	LDY	LAMTWN
	LDX	#16
	JSR	MVICON

;joystick handler

X15	LDA	STRIG0
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	ORA	STICK0
	CMP	OLDSTK
	BEQ	X50
	STA	OLDSTK
	LDY	#0
	STY	ATTRACT
	STY	AUDC1
	LDX	#7
LOOP50	CMP	DECODT,X
	BEQ	X51
	DEX
	BPL	LOOP50
	AND	#$F
	CMP	#$F
	BNE	X28
	LDY	LAMTWN
	LDX	#16
	JSR	MVICON
	JMP	X50

DECODT	DB	$E,$D,$B,7
	DB	$1E,$1D,$1B,$17

;error condition---bleep speaker
;this should be a nose crunch someday

X28	LDA	#$A8
	STA	AUDC1
	LDA	#$80
	STA	AUDF1
	JMP	X20

X50	JMP	X20
X51	TXA
	TAY
	LDX	APU
	LDA	ORDCNT,X
	CMP	#$1F
	BEQ	X28

;test for syntactical correctness

	LDX	LAMTWN
	CPY	#4
	BNE	X53
	LDA	UPCONT,X
	JMP	X57
X53	CPY	#5
	BNE	X54
	LDA	DNCONT,X
	JMP	X57
X54	CPY	#6
	BNE	X55
	LDA	LTCONT,X
	JMP	X57
X55	CPY	#7
	BEQ	X56
	LDA	LAMTWN
	STA	TEMPI
	JMP	X58
X56	LDA	RTCONT,X
X57	BMI	X28
	AND	#$1F
	STA	TEMPI
X58	TYA
	JSR	FIVASL
	ORA	TEMPI	;this is the order
	STA	TEMPI

;wipe out PRVTWN

	LDY	PRVTWN
	BMI	X82
	LDX	TWNOWN,Y
	JSR	MVICON

;store new order

X82	LDX	APU
	TXA
	JSR	FIVASL
	ORA	ORDCNT,X
	TAY
	LDA	TEMPI
	STA	ORDERS,Y
	INC	ORDCNT,X

;show orders icon in old LAMTWN

	AND	#$1F
	LDY	LAMTWN
	STY	PRVTWN
	STA	LAMTWN
	LDA	TEMPI
	AND	#$E0
	LSR	A
	LSR	A
	LSR	A
	LSR	A
	LSR	A
	ORA	#8
	TAX
	JSR	MVICON

;if order was movement, put llama into new town

	LDA	TEMPI
	BPL	X20
	LDY	LAMTWN
	LDX	#16
	JSR	MVICON

;display new llama town name

	LDA	#0
	LDX	#$4F
LOOP16	STA	TXTWDW,X
	DEX
	CPX	#$27
	BPL	LOOP16

	LDY	LAMTWN
	LDA	#$29
	JSR	DTWNAM

;is SELECT key depressed?
;(next APU)

X20	DEC	SELCLK
	BPL	X30
	INC	SELCLK
	LDA	CONSOL
	AND	#2
	BEQ	X29
X30	LDA	NAPFLG
	BMI	X91
	JMP	X22

X91	LDX	APU
	DEC	ORDCNT,X
	BPL	Y00
	INC	ORDCNT,X
	LDA	#0
	BEQ	Y01
Y00	TXA
	JSR	FIVASL
	STA	TEMPI
	TAY
	INY
LOOP27	LDA	ORDERS,Y
	BMI	Y02
	DEC	ORDCNT,X
	BPL	Y03
	INC	ORDCNT,X
	JMP	Y02
Y03	INY
	TYA
	AND	#$1F
	BNE	LOOP27
Y02	LDX	TEMPI
LOOP24	LDA	ORDERS,Y
	STA	ORDERS,X
	INY
	INX
	TYA
	AND	#$1F
	BNE	LOOP24
Y01	STA	NAPFLG
	JMP	X90

X29	STA	ATTRACT
	LDA	#30	;debounce delay
	STA	SELCLK

;dispatch messenger if APU is an Inca

	LDX	APU
	CPX	#4
	BCS	X47
	LDY	ORDCNT,X
	BEQ	X47	;don't send empty messengers
	STY	TEMPI
	LDX	#$F
LOOP18	LDA	L2MGTM,X
	BNE	X48
	LDA	H2MGTM,X
	BEQ	X49
X48	DEX
	BPL	LOOP18
	BMI	X47	;error: no messengers left

X49	LDA	APU
	STA	MSGRAP,X
	TYA
	STA	MSGRCT,X
	LDA	APUTWN	;Inca's position
	STA	MSGRTN,X
	STA	MSGRGO,X
	DCALC	2GMCK.=.2MGTM,X
	LDA	#0
	STA	MSGRDY,X
	STA	MSGPTR+1
	TXA
	JSR	FIVASL
	ROL	MSGPTR+1
	CLC
	ADC	#LOW MSGMSG
	STA	MSGPTR
	LDA	MSGPTR+1
	ADC	#HIGH MSGMSG
	STA	MSGPTR+1
	LDA	APU
	JSR	FIVASL
	ORA	TEMPI
	TAX
LOOP19	LDA	ORDERS,X
	STA	(MSGPTR),Y
	DEX
	DEY
	BPL	LOOP19

X47	LDX	APU
	INX
	CPX	#9
	BNE	X21
	LDX	#0
X21	STX	APU

;clean out old cursor town

X90	LDX	CURTWN
	LDA	#$FF
	STA	TWNMSK,X

;clean out old steptown

	LDY	STPTWN
	LDX	TWNOWN,Y
	JSR	MVICON

;clear out previous town

	LDY	PRVTWN
	BMI	X81
	LDX	TWNOWN,Y
	JSR	MVICON

X81	JSR	NEWAPU

;clean out hiker

	LDA	#$FF
	STA	TICK
	JSR	CLRMIS

;is START key depressed?
;(delete one command)

X22	DEC	STACLK
	BPL	X34
	INC	STACLK
	LDA	CONSOL
	AND	#1
	BNE	X34
	LDA	#30
	STA	STACLK
	LDA	#0
	STA	ATTRACT
	LDX	APU
	TXA
	AND	#4
	BNE	X34
	DEC	ORDCNT,X
	BPL	X19
	INC	ORDCNT,X
	JMP	X34

X19	LDY	LAMTWN
	LDX	TWNOWN,Y
	JSR	MVICON
	LDX	APU
	TXA
	JSR	FIVASL
	ORA	ORDCNT,X
	TAY
	DEY
	LDA	ORDERS,Y
	AND	#$1F
	LDY	ORDCNT,X
	BNE	X63
	LDA	CURTWN

X63	STA	LAMTWN
	LDY	#$4F
	LDA	#0
LOOP61	STA	TXTWDW,Y
	DEY
	CPY	#$27
	BNE	LOOP61

	LDY	LAMTWN
	LDA	#$29
	JSR	DTWNAM
	LDY	LAMTWN
	LDX	#16
	JSR	MVICON
X34
	JMP	XITVBV

DWORDS	LDA	WRDTAB,X
	PHP
	AND	#$7F
	SEC
	SBC	#$20
	STA	TXTWDW,Y
	INX
	INY
	PLP
	BPL	DWORDS
	RTS

DTWNAM	LDX	TWNNDX,Y
	TAY
LOOP22	LDA	TWNAM,X
	PHP
	AND	#$7F
	SEC
	SBC	#$20
	STA	TXTWDW,Y
	INX
	INY
	PLP
	BPL	LOOP22
	RTS

;sets up a new APU
NEWAPU
;first clear TXTWDW
	LDA	#0
	LDY	#$A0
LOOP06	STA	TXTWDW-1,Y
	DEY
	BNE	LOOP06
	DEY
	STY	STPCNT

;display APU name

	LDX	APU
	LDA	NAMNDX,X
	TAX
	LDY	#1
	JSR	DWORDS

;display APU men

	LDX	APU
	DCALC	2AMEN,X.=.2NMBR
	LDY	#14
	JSR	DDNMBR

;display APU town

	LDX	APU
	LDY	APUTWN,X
	STY	CURTWN
	STY	STPTWN
	LDA	#0
	STA	TWNMSK,Y

	LDA	#20
	JSR	DTWNAM
	LDY	LAMTWN
	LDX	TWNOWN,Y
	JSR	MVICON
	LDX	APU
	LDA	ORDCNT,X
	BNE	X25
	LDY	CURTWN
	JMP	X31
X25	TXA
	JSR	FIVASL
	ORA	ORDCNT,X
	TAY
	DEY
	LDA	ORDERS,Y
	AND	#$1F
	TAY

X31	STY	LAMTWN
	LDA	APU	;Spaniards don't get llamas
	AND	#4
	BNE	X32
	LDX	#16
	JSR	MVICON
	LDY	LAMTWN
	LDA	#$29
	JSR	DTWNAM
	LDY	#33
	LDX	CURTWN
	DCALC	2TMEN,X.=.2NMBR
	JSR	DDNMBR
X32	RTS

PATCHX	DB	2,3,1,0





;LOT07O.ASM  MARCH 6, 1984
;orders processing code

AOPROC	LDX	#7
LOOP96	STX	MLNAPU
	DCMP	2APUT,X.2GMCK
	BCC	X94	;time to act?
X52	JMP	ELP16

X94	TXA	;yes
	JSR	FIVASL
	TAX
	LDA	APUORD,X	;this is the order to execute
	AND	#$1F
	STA	SAVTWN
	TAY
	LDA	APUORD,X
	BPL	X95
	LDA	ATNOWN,Y	;who is in the town?
	AND	#4
	EOR	MLNAPU
	AND	#4
	BEQ	X96	;friend or foe?
	LDA	#0	;foe
	STA	ROADFG
	JSR	COMBAT
	BEQ	X52
	BCC	X52
X96	LDA	SAVTWN
	LDX	MLNAPU
	JSR	MAKNWS
	LDY	SAVTWN
	LDX	MLNAPU
	LDA	OWNCOD,X
	CMP	ATNOWN,Y
	BCS	X97
	STA	ATNOWN,Y
X97	TYA
	STA	TRUPOS,X
	LDX	MLNAPU
	LDA	#$FF
	STA	AFMTWN,X
	STA	ATOTWN,X
	JMP	X98	;setup next order

X95	LDX	MLNAPU
	ASL	A
	BMI	X39	;mode order?
	ASL	A	;change men
	BMI	X36
	STX	SAVEX
	TYA
	TAX
	DLSR	2TMEN,X	;pickup men
	LDX	SAVEX
	BCC	X37
	DINC	2AMEN,X
X37
	DCALC	2TMEN,Y.+.2AMEN,X.=.2AMEN,X
	JMP	X98	;setup next order

X36
	DLSR	2AMEN,X	;drop men
	BCC	X38
	STX	SAVEX
	TYA
	TAX
	DINC	2TMEN,X
	LDX	SAVEX
X38
	DCALC	2AMEN,X.+.2TMEN,Y.=.2TMEN,Y
	JMP	X98	;setup next order

X39	ASL	A	;mode order
	BMI	X41	;which way?
	DEC	APUSPD,X	;left, slower
	BPL	X98
	INC	APUSPD,X
	JMP	X98	;setup next order
X41	LDA	APUSPD,X
	CMP	#7
	BEQ	X98
	INC	APUSPD,X

X98	LDX	MLNAPU	;setup the next order
	DEC	APORCT,X
	BNE	X42	;any orders left?
	LDA	#$FF
	STA	L2APUT,X
	STA	H2APUT,X
	JMP	ELP16	;no
X42	TXA	;yes
	JSR	FIVASL
	TAY
	ORA	APORCT,X
	STA	TEMP2

;(TEMP2 points to highest filled slot in orders queue)

LOOP97	LDA	APUORD+1,Y	;collapse stack
	STA	APUORD,Y
	INY
	CPY	TEMP2
	BCC	LOOP97

	TYA
	AND	#$E0
	TAY	;point back to current order

	JSR	CALOTM

ELP16	LDX	MLNAPU
	DEX
	BMI	X43
	JMP	LOOP96
X43	RTS

;calculates the time required for execution of next order
;input:   Y: APU X 32
;         MLNAPU

CALOTM	LDX	MLNAPU
	LDA	APUORD,Y	;get that order
	BMI	X72	;travel order?
	RTS	;no, stationary order

X72	AND	#$1F
	STA	ATOTWN,X
	LDA	APUORD,Y
	AND	#$60
	ORA	TRUPOS,X
	TAY
	DCALC	TTBTWN,Y
	DASL	2PROD
	DASL	2PROD
	DASL	2PROD
	DASL	2PROD
	DASL	2PROD
	DCALC	+.2GMCK.=.2APUT,X
	LDA	TRUPOS,X
	STA	AFMTWN,X
	STA	TEMP2
	LDA	#$FF
	STA	TRUPOS,X

;reset ownership of abandoned town

	TXA
	AND	#4
	TAX
	LDY	#2
LOOP98	LDA	TRUPOS,X
	CMP	TEMP2
	BNE	X84
	LDY	#1
	TXA
	AND	#3
	BNE	X84
	LDY	#0
X84	INX
	TXA
	AND	#3
	BNE	LOOP98
	STY	TEMP3
	LDY	TEMP2
	LDA	MLNAPU
	AND	#4
	ORA	TEMP3
	STA	ATNOWN,Y

;check for road engagements

	LDA	MLNAPU
	TAX
	AND	#4
	EOR	#4
	TAY
LOOP99	LDA	AFMTWN,Y
	CMP	ATOTWN,X
	BNE	X87
	LDA	ATOTWN,Y
	CMP	AFMTWN,X
	BNE	X87
	LDA	#$FF
	STA	ROADFG
	LDA	ATOTWN,X
	STA	SAVTWN
	JSR	COMBAT
	LDY	DEFNDR
X87	INY
	TYA
	AND	#3
	BNE	LOOP99
	RTS


OWNCOD	DB	0,1,1,1,4,5,5,5
L2STOT	DB	0,$30,$10,$10
H2STOT	DB	1,0,0,0




;LOT07L.ASM  FEBRUARY 28, 1984
;DLI service routine
;
DLISRV	PHA
	TXA
	PHA
	TYA
	PHA
	LDA	DLICNT
	CMP	#7
	BCS	X24
	ASL	A
	ASL	A
	TAY
	LDX	#0
LOOP08	LDA	XTOWN,Y
	STA	HPOSP0,X
	LDA	TWNCOL,Y
	AND	TWNMSK,Y
	EOR	COLRSH
	AND	DRKMSK
	STA	COLPM0,X
	INY
	INX
	CPX	#4
	BNE	LOOP08
	LDX	DLICNT
	INX
	CPX	#6
	BNE	X13
	LDA	#$92
	EOR	COLRSH
	AND	DRKMSK
	STA	COLBK
	JMP	X13
;
X24	CMP	#7
	BNE	X12
	LDA	#$44
	EOR	COLRSH
	AND	DRKMSK
	TAX
	LDA	#$4E
	EOR	COLRSH
	AND	DRKMSK
	STX	COLPF2
	STA	COLPF1
	LDX	#8
	JMP	X13
;
X12	CMP	#8
	BNE	X27
	LDA	#$3A
	EOR	COLRSH
	AND	DRKMSK
	TAX
	LDA	#0
	EOR	COLRSH
	AND	DRKMSK
	STX	COLPF2
	STA	COLPF1
	LDX	#9
	JMP	X13
;
X27	LDA	#$C6
	EOR	COLRSH
	AND	DRKMSK
	TAX
	LDA	#$CE
	EOR	COLRSH
	AND	DRKMSK
	STX	COLPF2
	STA	COLPF1
	LDX	#0
;
X13	STX	DLICNT
	PLA
	TAY
	PLA
	TAX
	PLA
	RTI






;LOT07D.ASM  FEBRUARY 28, 1984
;DATA TABLES

ICNMAP
	DB	0,0,0,$78
	DB	$78,$78,0,0
	DB	0,$78,$78,$78
	DB	$78,$78,$78,0
	DB	$78,$78,$78,$78
	DB	$78,$78,$78,$78
	DB	$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF

	DB	0,0,0,$78
	DB	$78,$78,0,0
	DB	0,$78,$78,$78
	DB	$78,$78,$78,0
	DB	$78,$78,$78,$78
	DB	$78,$78,$78,$78
	DB	$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF

	DB	$10,$10,$38,$38
	DB	$7C,$7C,$10,$10
	DB	$10,$10,$7C,$7C
	DB	$38,$38,$10,$10
	DB	0,$10,$30,$78
	DB	$78,$30,$10,0
	DB	0,$20,$30,$78
	DB	$78,$30,$20,0

	DB	$10,$10,$38,$38
	DB	$7C,$7C,0,0
	DB	0,0,$7C,$7C
	DB	$38,$38,$10,$10
	DB	0,$10,$30,$70
	DB	$70,$30,$10,0
	DB	0,$20,$30,$38
	DB	$38,$30,$20,0

	DB	$40,$C0,$48,$78
	DB	$78,$48,$48,$48
	DB	0,0,0,0
	DB	0,0,0,0

ICNCOL	DB	$28,$28,$28,$28
	DB	$C6,$C6,$C6,$C6
	DB	$94,$94,$94,$94
	DB	$64,$64,$64,$64
	DB	0,0

YTOWN	DB	42,43,31,31
	DB	54,55,57,54
	DB	74,74,75,75
	DB	89,86,85,94
	DB	104,132,121,113
	DB	147,152,147,145
	DB	166,174,165,165

XTOWN	DB	133,146,158,190
	DB	86,115,166,193
	DB	110,144,164,187
	DB	62,93,130,145
	DB	128,152,172,193
	DB	65,77,129,192
	DB	101,127,154,0

TWNOWN	DB	2,5,4,4
	DB	4,4,4,4
	DB	4,4,4,4
	DB	4,4,4,4
	DB	4,4,4,5
	DB	6,4,4,4
	DB	4,4,4,4

YDLI	DB	0,20,35,52,73,111,131,155
COLTAB	DB	$36,$36,$22,$22
	DB	$12,$20,$0C,$0,$36

UPCONT	DB	$FF,2,$FF,$FF
	DB	$FF,$FF,2,3
	DB	5,1,6,7
	DB	$FF,4,$40,9
	DB	14,$4F,$FF,11
	DB	12,$FF,$50,19
	DB	$4D,22,17,$FF
	DB	$FF,$FF,$FF,$FF
DNCONT	DB	$4E,9,6,7
	DB	13,8,10,11
	DB	$FF,15,$FF,19
	DB	20,$58,16,$51
	DB	$56,26,$FF,23
	DB	$FF,$FF,25,$FF
	DB	$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF
LTCONT	DB	$45,$40,1,2
	DB	$FF,$44,1,6
	DB	13,14,$49,10
	DB	$FF,12,8,$FF
	DB	$FF,$FF,17,18
	DB	$FF,20,$FF,26
	DB	21,24,25,$FF
	DB	$FF,$FF,$FF,$FF
RTCONT	DB	$41,6,3,$FF
	DB	$45,$40,7,$FF
	DB	14,$4A,11,$FF
	DB	13,8,9,$FF
	DB	$FF,18,19,$FF
	DB	21,24,$FF,$FF
	DB	25,26,23,$FF
	DB	$FF,$FF,$FF,$FF

APUTWN	DB	0,0,0,0
	DB	20,1,19,20,0

TWNAM	DC	'MACHU PICCHU'
	DC	'CUZCO'
	DC	'MACUSANI'
	DC	'AZANGARO'
	DC	'SATIPO'
	DC	'VITCOS'
	DC	'AYAVIRI'
	DC	'CHUCUITO'
	DC	'HUARI'
	DC	'ABANCAY'
	DC	'CAILLOMA'
	DC	'CHIVAY'
	DC	'TARMA'
	DC	'SAUSA'
	DC	'VILCASHUAMAN'
	DC	'TORAYA'
	DC	'JINCAMOCCO'
	DC	'INCAHUASI'
	DC	'CHUQUIBAMBA'
	DC	'AREQUIPA'
	DC	'LIMA'
	DC	'PACHACAMAC'
	DC	'NAZCA'
	DC	'UNCONA'
	DC	'PARACAS'
	DC	'ICA'
	DC	'ATIQUIPA'
WRDTAB	DC	'MANCO CAPAC'
	DC	'YAMQUI MAYTA'
	DC	'CUSI PUMA'
	DC	'HUALPA'
	DC	'FRANCISCO'
	DC	'HERNANDO'
	DC	'GONZALO'
	DC	'JUAN'
	DC	'QUIPUCAMAYOC'
TWNNDX	DB	0,12,17,25
	DB	33,39,45,52
	DB	60,65,72,80
	DB	86,91,96,108
	DB	114,124,133,144
	DB	152,156,166,171
	DB	177,184,187,194
	DB	194,194,194,194
NAMNDX	DB	0,11,23,32
	DB	38,47,55,62,66

TTBTWN	DB	0,43,0,0
	DB	0,0,50,32
	DB	31,51,21,42
	DB	0,76,103,21
	DB	24,75,0,70
	DB	130,0,90,37
	DB	212,34,49,0
	DB	0,0,0,0

	DB	103,51,50,32
	DB	76,31,21,42
	DB	0,21,0,70
	DB	130,212,24,75
	DB	90,49,0,37
	DB	0,0,34,0
	DB	0,0,0,0
	DB	0,0,0,0

	DB	66,39,43,68
	DB	0,121,51,54
	DB	44,40,77,61
	DB	0,64,52,0
	DB	0,0,35,33
	DB	0,23,0,67
	DB	43,37,41,0
	DB	0,0,0,0

	DB	39,51,68,0
	DB	121,66,54,0
	DB	52,77,61,0
	DB	64,44,40,0
	DB	0,35,33,0
	DB	23,43,0,0
	DB	37,41,67,0
	DB	0,0,0,0

IEXPNC	DB	32,16,16,16
	DB	255,128,128,128,8,64




;LOT07R.ASM  FEBRUARY 22, 1984
;ROAD TRAVERSING ROUTINES
;
CLRMIS	LDA	HIKEY
	SEC
	SBC	#6
	TAY
	LDX	#12
	LDA	#0
LOOP00	STA	MISSLE,Y
	INY
	DEX
	BPL	LOOP00
	STA	AUDF4
	STA	AUDC4
	RTS
;
DRWMIS	LDA	TICK
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	TAX
	LDA	HIKEY
	SEC
	SBC	#8
	TAY
LOOP01	LDA	MISTAB,X
	STA	MISSLE,Y
	INX
	INY
	TXA
	AND	#$F
	BNE	LOOP01

	LDA	TICK
	ASL	A
	ASL	A
	TAX
	LDY	#0
LOOP02	LDA	HIKEX
	CLC
	ADC	MISSUB,X
	STA	HPOSM0,Y
	INX
	INY
	CPY	#4
	BNE	LOOP02

;hiker color change

	LDY	TICK
	LDA	TCKCOL,Y
	STA	COLOR3
	STA	COLPF3

;hiker grumbling

	LDA	TCKFRQ,Y
	STA	AUDF4
	LDA	TCKVOL,Y
	STA	AUDC4

	RTS

TCKFRQ	DB	$F0,$A0,$60,$10
TCKVOL	DB	$22,$44,$66,$88
TCKCOL	DB	$FF,$88,$54,0
MISTAB
	DB	0,0,0,0
	DB	0,0,2,2
	DB	$A0,$A0,8,8
	DB	0,0,0,0

	DB	0,0,0,0
	DB	0,0,2,2
	DB	$A0,$A0,8,8
	DB	0,0,0,0

	DB	0,0,0,0
	DB	0,0,$82,$82
	DB	0,0,$28,$28
	DB	0,0,0,0

	DB	0,0,0,0
	DB	2,2,0,0
	DB	$A0,$A0,0,0
	DB	8,8,0,0

MMASK
	DB	4,0,0,0
	DB	4,0,4,4
	DB	4,0,0,4
	DB	4,0,4,4

	DB	0,4,0,0
	DB	0,4,4,4
	DB	0,4,4,0
	DB	0,4,4,4

	DB	0,0,4,0
	DB	4,4,4,0
	DB	4,0,4,0
	DB	4,4,4,0

	DB	0,0,0,4
	DB	4,4,0,4
	DB	0,4,0,4
	DB	4,4,0,4

MISSUB
	DB	0,0,$FF,1
	DB	0,0,$FF,1
	DB	$FF,1,$FF,1
	DB	0,0,$FE,2

XOFFTB	DB	0,0,$FF,1
YOFFTB	DB	$FE,2,0,0

;LAST OF THE INCAS MAINLINE
;LOT07M  FEBRUARY 28, 1984
;
ILIST	=	0
MULORG	=	*
SQMUL	=	0
	INCLUDE	D2:CIOMAC.ASM
	INCLUDE	D2:CALCMAC.ASM
	LIST	L,I,-M
	INCLUDE	D3:LOT07E.ASM





	ORG	$5000
	INCLUDE	D3:LOT07B.ASM
;
;The AI and command execution routines go here
;
HANG	JSR	MSGREX
	JSR	SCNWSQ
	JSR	AOPROC
	JMP	HANG
;
	INCLUDE	D3:LOT07I.ASM	;VBI
	INCLUDE	D3:LOT07L.ASM	;DLI
	INCLUDE	D3:LOT07S.ASM	;subs
	INCLUDE	D3:LOT07D.ASM	;data
	INCLUDE	D3:LOT07R.ASM	;hike
	INCLUDE	D3:LOT07N.ASM	;news
	INCLUDE	D3:LOT07O.ASM	;orders
	INCLUDE	D3:LOT07C.ASM	;combat
	INCLUDE	D3:LOT07G.ASM	;messenger
	END	INIT





;LOT07E.ASM  FEBRUARY 28, 1984

	ORG	$80

;variables written to by VBI or DLI

DLICNT	ORG	*+1	;DLI counter
CURTWN	ORG	*+1	;cursor town
OLDSTK	ORG	*+1	;previous stick value
TEMPI	ORG	*+1	;temporary for interrupt
SELCLK	ORG	*+1	;select key debounce timer
APU	ORG	*+1	;APU currently SELECTed
LAMTWN	ORG	*+1	;town llama is on
SAVXTN	ORG	*+1	;???
STPCNT	ORG	*+1	;counter for hiker town
STPTWN	ORG	*+1	;town hiker is going to
STACLK	ORG	*+1	;START key debounce timer
DCNTL	ORG	*+1	;zero suppress flag for DDNUMBR
L2PMPT	ORG	*+1	;player-missile pointer
H2PMPT	ORG	*+1
HIKEX	ORG	*+1	;hiker coordinates
HIKEY	ORG	*+1
TICK	ORG	*+1	;hiker state (0-3)
DESTWN	ORG	*+1	;hiker destination
DIR	ORG	*+1	;hiker direction
VRBTNX	ORG	*+1	;verboten direction
PRVTWN	ORG	*+1	;previous town
TRVLTM	ORG	*+1	;travel time
TCKDEL	ORG	*+1	;tick delay
L2NMBR	ORG	*+1	;input for DDNMBR
H2NMBR	ORG	*+1

;variables used by mainline

L2PTR	ORG	*+1	;general purpose pointer
H2PTR	ORG	*+1
TEMP1	ORG	*+1	;general purpose variables
TEMP2	ORG	*+1
TEMP3	ORG	*+1
TEMP4	ORG	*+1
L2GMCK	ORG	*+1	;game clock
H2GMCK	ORG	*+1
NWQPTR	ORG	*+1	;news queue pointer
KNWPTR	ORG	*+2	;pointer to knowledge array
MLNAPU	ORG	*+1	;mainline APU
ROADFG	ORG	*+1	;road flag
SAVEX	ORG	*+1	;saves X-register
ERRFLG	ORG	*+1	;error flag
SAVTWN	ORG	*+1	;save town
ATACKR	ORG	*+1	;combat routine
DEFNDR	ORG	*+1	;combat routine
SRCAPU	ORG	*+1	;search APU
SRCEVT	ORG	*+1	;search event
MSGRNM	ORG	*+1	;messenger number
MSGPTR	ORG	*+2	;pointer for messages
NWSCNT	ORG	*+1	;how many news items exist
NAPFLG	ORG	*+1	;new APU flag

;this region is for uninitialized tables.
;It should eventually be moved to the DUP area.
;Only DS pseudo-ops are allowed here

	ORG	$3800

;as PERCEIVED by the leader (Manco or Francisco)
ORDERS	DS	256	;orders
ORDCNT	DS	9	;order count
TWNMSK	DS	27	;town shape
TWNCOL	DS	27	;town color
APUMOD	DS	9	;mode
L2AMEN	DS	9	;APU men
H2AMEN	DS	9
L2TMEN	DS	27
H2TMEN	DS	27

;as PERCEIVED by APU

APUORD	DS	256	;orders
TRUPOS	DS	9	;position
APUSPD	DS	9	;speed
APORCT	DS	9	;orders count
AFMTWN	DS	9	;travelling from
ATOTWN	DS	9	;travelling to
AEXPNC	DS	10	;experience
L2APUT	DS	9
H2APUT	DS	9
ATNOWN	DS	27	;actual owner of town

;news events database

NWSDAY	DS	256	;date of news event
NWSAPU	DS	256	;perpetrator in D5-D7, town in D0-D4
NWSKNW	DS	$400	;what towns know?

;news queue database

L2NWQT	DS	256	;time of notification
H2NWQT	DS	256
NWQTWN	DS	256	;news queue town
NWQEVT	DS	256	;news queue event

;messenger database

MSGRAP	DS	32	;sought APU
MSGRTN	DS	32	;current location
L2MGTM	DS	32	;time of arrival
H2MGTM	DS	32
MSGMSG	DS	1024	;message set
MSGRDY	DS	32	;date of last known APU position
MSGRGO	DS	32	;immediate destination
MSGRCT	DS	32	;carried orders count

	ASSERT	* < $5000
	ORG	$9800
PMTAB
	LIST	-L
DLIST	DB	$70,$F0,$50
	DB	$4E
	DW	MAPDAT
	ECHO	101
	DB	$E
	ENDM
	DB	$4E
	DW	MAPDAT+$FF0
	ECHO	56
	DB	$E
	ENDM
	DB	$C2
	DW	TXTWDW
	DB	$10,$82,$10,2,2
	DB	$41
	DW	DLIST
TXTWDW	DS	120

;note there is some free space here

	ORG	PMTAB+$300
MISSLE	DS	256
PLAYR0	DS	256
PLAYR1	DS	256
PLAYR2	DS	256
PLAYR3	DS	256
MAPDAT	=	$A010
;
RTCLKL	EQU	$14
ATTRACT	EQU	$4D
DRKMSK	EQU	$4E
COLRSH	EQU	$4F
VDSLST	EQU	$200
VKEYBD	EQU	$208
SDMCTL	EQU	$22F
SDLSTL	EQU	$230
SDLSTH	EQU	$231
GPRIOR	EQU	$26F
STICK0	EQU	$278
STRIG0	EQU	$284
PCOLR0	EQU	$2C0
PCOLR1	EQU	$2C1
PCOLR2	EQU	$2C2
PCOLR3	EQU	$2C3
COLOR0	EQU	$2C4
COLOR1	EQU	$2C5
COLOR2	EQU	$2C6
COLOR3	EQU	$2C7
COLOR4	EQU	$2C8
CH	EQU	$2FC
SETVBV	EQU	$E45C
XITVBV	EQU	$E462
HPOSP0	EQU	$D000
M0PF	EQU	$D000
HPOSM0	EQU	$D004
HPOSM1	EQU	$D005
HPOSM2	EQU	$D006
HPOSM3	EQU	$D007
SIZEP0	EQU	$D008
SIZEP1	EQU	$D009
SIZEP2	EQU	$D00A
SIZEP3	EQU	$D00B
SIZEM	EQU	$D00C
TRIG0	EQU	$D010
COLPM0	EQU	$D012
COLPF0	EQU	$D016
COLPF1	EQU	$D017
COLPF2	EQU	$D018
COLPF3	EQU	$D019
COLBK	EQU	$D01A
PRIOR	EQU	$D01B
GRACTL	EQU	$D01D
HITCLR	EQU	$D01E
CONSOL	EQU	$D01F
AUDF1	EQU	$D200
AUDC1	EQU	$D201
AUDF2	EQU	$D202
AUDC2	EQU	$D203
AUDF3	EQU	$D204
AUDC3	EQU	$D205
AUDF4	EQU	$D206
AUDC4	EQU	$D207
AUDCTL	EQU	$D208
KBCODE	EQU	$D209
RANDOM	EQU	$D20A
DMACTL	EQU	$D400
PMBASE	EQU	$D407
WSYNC	EQU	$D40A
VCOUNT	EQU	$D40B
NMIEN	EQU	$D40E
	LIST	*






;LOT07S.ASM  FEBRUARY 28, 1984
;SUBROUTINES

;SUBROUTINE DNUMBR

DNUMBR	LDX	#$FF
	STX	DCNTL
	SEC
LOOP43	INX
	SBC	#100
	BCS	LOOP43
	PHA
	TXA
	BEQ	X07
	ADC	#$10
	STA	TXTWDW,Y
	INY
	INC	DCNTL
X07	PLA
	ADC	#100
	LDX	#$FF
	SEC
LOOP44	INX
	SBC	#10
	BCS	LOOP44
	PHA
	TXA
	BNE	X08
	BIT	DCNTL
	BMI	X09
X08	ADC	#$10
	STA	TXTWDW,Y
	INY
X09	PLA
	ADC	#$1A
	STA	TXTWDW,Y
	INY
	LDA	#0
	STA	TXTWDW,Y
	INY
	RTS

;SUBROUTINE DDNMBR

DDNMBR	LDX	#$FF
	STX	DCNTL
	SEC
LOOP57	INX
	DCALC	2NMBR.-.2NMYR.=.2NMBR
	BCS	LOOP57
	TXA
	BEQ	X00
	ADC	#$10
	STA	TXTWDW,Y
	INY
	INC	DCNTL
X00
	DCALC	2NMBR.+.2NMYR.=.2NMBR
	LDX	#$FF
	SEC

LOOP58	INX
	DCALC	2NMBR.-.2NMIL.=.2NMBR
	BCS	LOOP58
	TXA
	BNE	X01
	BIT	DCNTL
	BMI	X02
X01	ADC	#$10
	STA	TXTWDW,Y
	INY
	INC	DCNTL
X02
	DCALC	2NMBR.+.2NMIL.=.2NMBR
	LDX	#$FF
	SEC

LOOP56	INX
	DCALC	2NMBR.-.#100.=.2NMBR
	BCS	LOOP56
	TXA
	BNE	X03
	BIT	DCNTL
	BMI	X35
X03	ADC	#$10
	STA	TXTWDW,Y
	INY
	INC	DCNTL
X35	LDA	L2NMBR
	ADC	#100
	LDX	#$FF
	SEC

LOOP59	INX
	SBC	#10
	BCS	LOOP59
	PHA
	TXA
	BNE	X05
	BIT	DCNTL
	BMI	X06
X05	ADC	#$10
	STA	TXTWDW,Y
	INY
X06	PLA
	ADC	#$1A
	STA	TXTWDW,Y
	INY
	RTS
L2NMIL	DB	$E8
H2NMIL	DB	$3
L2NMYR	DB	$10
H2NMYR	DB	$27

;requires target town in Y
;and icon number in X

MVICON	LDA	ICNCOL,X
	STA	TWNCOL,Y
	TXA
	ASL	A
	ASL	A
	ASL	A
	TAX
	TYA
	AND	#3
	ADC	#[HIGH PMTAB] + 4
	STA	H2PMPT
	LDA	YTOWN,Y
	TAY
LOOP17	LDA	ICNMAP,X
	STA	(L2PMPT),Y
	INX
	INY
	TXA
	AND	#7
	BNE	LOOP17
	RTS

ABSVAL	BPL	X33
	EOR	#$FF
	CLC
	ADC	#1
X33	RTS

FIVASL	ASL	A
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	RTS





;LOT05C.ASM  FEBRUARY 12, 1984
;COMBAT SUBROUTINE

;Inputs:
;	MLNAPU is attacker
;	SAVTWN specifies defender
;	ROADFG<>0 => road engagement

;Outputs:
;	2AMEN reduced for both sides
;	2AMEN = 0 will kill APU
;	Z=1 => standoff, fight again
;	C=1 => attacker wins
;	C=0 => defender wins

COMBAT	SEC
	LDA	#$FF
	RTS	;pretty snazzy, huh?






;LOT07B.ASM  FEBRUARY 28, 1984
;displays map with DLI's and players
;performs all game initialization
;load in map file

INIT	CLD
	OPEN	3.4.0.D3:MAP24.IMA
	GETBYT	3.MAPDAT.MAPDAT+7680
	CLOSE	3

;fix up display list with DLI's

	LDA	#$40
	STA	NMIEN
	LDX	#7
LOOP04	LDY	YDLI,X
	CPY	#108
	BCC	X16
	INY
	INY
X16	LDA	DLIST+5,Y
	ORA	#$80
	STA	DLIST+5,Y
	DEX
	BNE	LOOP04

;set up display list and PM controls

	SETVEC	DLIST.SDLSTL
	LDA	#$3E
	STA	SDMCTL
	LDA	#3
	STA	GRACTL
	LDA	#$11
	STA	GPRIOR
	LDA	#HIGH PMTAB
	STA	PMBASE

	LDX	#8
LOOP05	LDA	COLTAB,X
	STA	PCOLR0,X
	DEX
	BPL	LOOP05

;clear out players

	LDA	#0
	LDX	#0
LOOP03	STA	MISSLE,X
	STA	PLAYR0,X
	STA	PLAYR1,X
	STA	PLAYR2,X
	STA	PLAYR3,X
	STA	NWSKNW,X
	STA	NWSKNW+256,X
	STA	NWSKNW+512,X
	STA	NWSKNW+768,X
	INX
	BNE	LOOP03

	STA	SIZEP0
	STA	SIZEP1
	STA	SIZEP2
	STA	SIZEP3
	STA	SIZEM
	STA	CURTWN
	STA	APU
	STA	SELCLK
	STA	LAMTWN
	STA	STPTWN
	STA	L2PMPT
	STA	NWQPTR
	STA	NWSCNT
	STA	L2GMCK
	STA	H2GMCK
	TAY
	LDA	XTOWN
	STA	SAVXTN
	LDA	#$FF
	STA	TICK

;initialize towns

	LDY	#27
LOOP14	STY	TEMP1
	LDX	TWNOWN,Y
	TXA
	STA	ATNOWN,Y
	JSR	MVICON
	LDY	TEMP1
	LDA	#$FF
	STA	TWNMSK,Y
	LDA	#0
	STA	H2TMEN,Y
	LDA	RANDOM
	AND	#$3F
	STA	L2TMEN,Y
	DEY
	BPL	LOOP14

;initialize APU's

	LDX	#8
LOOP15	LDA	#0
	STA	ORDCNT,X
	STA	H2AMEN,X
	STA	APUSPD,X
	STA	APORCT,X
	LDA	#$FF
	STA	L2APUT,X
	STA	H2APUT,X
	STA	AFMTWN,X
	STA	ATOTWN,X
	LDA	IEXPNC,X
	STA	AEXPNC,X
	LDA	APUTWN,X
	STA	TRUPOS,X
	LDA	#0
	CPX	#8
	BEQ	X10
	STX	SAVEX
	LDA	TRUPOS,X
	JSR	MAKNWS
	LDX	SAVEX
	LDA	RANDOM
	AND	#$1F
	CPX	#4
	BCC	X10
	ASL	A
X10	STA	L2AMEN,X
	DEX
	BPL	LOOP15

;display Manco

	LDA	#0
	STA	TWNMSK
	JSR	NEWAPU	;handle Manco Capac

;initialize messengers

	LDX	#$1F
	LDA	#0
LOOP09	STA	L2MGTM,X
	STA	H2MGTM,X
	STA	MSGRCT,X
	DEX
	BPL	LOOP09

	STA	NAPFLG

;publish the initial news

	LDY	#3
	LDA	#LOW NWSKNW
	STA	KNWPTR
LOOP26	LDA	#HIGH NWSKNW
	STA	KNWPTR+1
LOOP25	LDA	#$FF
	STA	(KNWPTR),Y
	INC	KNWPTR+1
	LDA	KNWPTR+1
	CMP	#[HIGH NWSKNW] + 4
	BNE	LOOP25
	DEY
	BPL	LOOP26

;enable VBI's

	LDX	#[HIGH VBISRV]
	LDY	#[LOW VBISRV]
	LDA	#7
	JSR	SETVBV

;now let loose the DLI's

	SETVEC	DLISRV.VDSLST
	LDA	#0
	STA	DLICNT
LOOP07	LDA	VCOUNT
	CMP	#12
	BCS	LOOP07
	LDA	#$C0
	STA	NMIEN

